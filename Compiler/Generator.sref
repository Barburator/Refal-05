$EENUM GN-Local, GN-Entry;

FuncArguments {
  = 'refalrts::Iter arg_begin, refalrts::Iter arg_end';
}

NakedArguments {
  = 'refalrts::Iter, refalrts::Iter';
}

TextFromMemoryClass {
  GN-Local = 'static '; GN-Entry = ;
}

SwGenFnStart {
  s.MemoryClass s.FnArguments e.Name =
    (
      <TextFromMemoryClass s.MemoryClass>
      'refalrts::FnResult ' e.Name '(' <s.FnArguments> ') {'
    )
    ('  refalrts::this_is_generated_function();');
}

$ENTRY GenFnStart {
  s.MemoryClass e.Name =
    <SwGenFnStart s.MemoryClass FuncArguments e.Name>;
}

$ENTRY GenFnEnd {
  = ('  return refalrts::cRecognitionImpossible;') ('}') ();
}

$ENTRY GenEEnum {
  e.Name = <SwGenFnStart GN-Entry NakedArguments e.Name> <GenFnEnd>;
}

$ENTRY GenEnum {
  e.Name = <SwGenFnStart GN-Local NakedArguments e.Name> <GenFnEnd>;
}

$ENTRY GenExtern {
  e.Name =
    ('extern refalrts::FnResult ' e.Name '(' <FuncArguments> ');') ();
}

$ENTRY GenForward {
  e.Name =
    ('static refalrts::FnResult ' e.Name '(' <FuncArguments> ');') ();
}

$FORWARD GenSwap-Aux;

$ENTRY GenESwap {
  e.Name =
    ('//$ESWAP ' e.Name)
    <GenSwap-Aux GN-Entry e.Name>;
}

$ENTRY GenSwap {
  e.Name =
    ('//$SWAP ' e.Name)
    <GenSwap-Aux GN-Local e.Name>;
}

GenSwap-Aux {
  s.MemoryClass e.Name =
    <GenFnStart s.MemoryClass e.Name>
    ('  refalrts::Iter func_name = arg_begin;')
    ()
    ('  refalrts::move_left( func_name, arg_end );')
    ()
    ('  refalrts::Iter info_b = func_name;')
    ('  refalrts::Iter info_e = arg_end;')
    ()
    ('  refalrts::move_left( info_b, info_e );')
    ('  refalrts::move_right( info_b, info_e );')
    ()
    ('  static refalrts::Iter s_head = refalrts::initialize_swap_head( func_name );')
    ()
    ('  refalrts::Iter saved_b;')
    ('  refalrts::Iter saved_e;')
    ()
    ('  refalrts::swap_info_bounds( saved_b, saved_e, s_head );')
    ('  refalrts::splice_evar( arg_begin, saved_b, saved_e );')
    ('  refalrts::swap_save( s_head, info_b, info_e );')
    ('  refalrts::splice_to_freelist( arg_begin, arg_end );')
    ()
    ('  return refalrts::cSuccess;')
    ('}')
    ();
}

$ENTRY GenIdent {
  e.Name =
    ('//$LABEL ' e.Name)
    ('template <typename T>')
    ('struct ' e.Name 'L_ {')
    ('  static const char *name() {')
    ('    return "' e.Name '";')
    ('  }')
    ('};')
    ();
}

$ENTRY GenCommonHeaders {
  = ('// Automatically generated file. Don\'t edit!')
  ('#include "refalrts.h"')
  () ();
}

$ENTRY GenCommonTailer {
  = () ('//End of file');
}

// Команды и их подтипы
//FROM Algorithm
$EXTERN CmdChar, CmdNumber, CmdName, CmdIdent,
  CmdBrackets, CmdADT, CmdEmpty,
  CmdRepeated, CmdVar, CmdOpenedE, CmdClosedE,
  CmdAllocateElem, CmdReinitElem,
  CmdLinkBrackets, CmdPushStack,
  CmdInsertElem, CmdInsertEVar,
  ElChar, ElName, ElNumber, ElIdent,
  ElOpenADT, ElCloseADT,
  ElOpenBracket, ElCloseBracket,
  ElOpenCall, ElCloseCall,
  ElElem, ElSTVar, ElSeq,
  CmdEmptyResult, CmdReturnResult,
  CmdIfDef, CmdElse, CmdEndIf,
  CmdInitRAA, CmdFinRAA,
  CmdDeclareEVar, CmdDeclareVar,
  CmdCopyEVar, CmdCopyVar,                             
  AlgLeft, AlgRight,
  CmdComment, CmdPrint,
  CmdArrChar, CmdArrInt, CmdArrFunc, CmdArrIdent,
  CmdArrOB, CmdArrCB, CmdArrOADT, CmdArrCADT,
  CmdArrOC, CmdArrCC, CmdArrSplice, CmdArrCopy,
  CmdDropIndent;

//FROM Algorithm_v2
$EXTERN CmdInner, CmdTerm, AlgMatch;


$FORWARD GenSentence-Aux, GenCommand, BracketsVar-B, BracketsVar-E;

//FROM LibraryEx
$EXTERN Map, MapReduce, Fetch;

//FROM Library
$EXTERN StrFromInt, WriteLine;

$ENTRY GenSentence {
  e.Algorithm =
    ('  do {')
    ('    refalrts::Iter ' <BracketsVar-B 0> ' = arg_begin;')
    ('    refalrts::Iter ' <BracketsVar-E 0> ' = arg_end;')
    ('    refalrts::move_left( ' <BracketsVar-B 0> ', ' <BracketsVar-E 0> ' );')
    ('    refalrts::move_left( ' <BracketsVar-B 0> ', ' <BracketsVar-E 0> ' );')
    ('    refalrts::move_right( ' <BracketsVar-B 0> ', ' <BracketsVar-E 0> ' );')
    <GenSentence-Aux
      <MapReduce
        GenCommand
        ('    ' /* indent */)
        e.Algorithm
      >
    >;
}

$FORWARD GenCloseBrackets;

GenSentence-Aux {
  (e.Indent) e.Generated =
    e.Generated
    //<GenCloseBrackets e.Indent>
    ();
}

BracketsVar-B {
  s.BracketNumber = 'bb_' <StrFromInt s.BracketNumber>;
}

BracketsVar-E {
  s.BracketNumber = 'be_' <StrFromInt s.BracketNumber>;
}

PosVar {
  s.Pos = 'n' <StrFromInt s.Pos>;
}

/*
  Величина отступа также определяет режим работы. Отступ в 4 пробела --
  вне цикла по открытой e-переменной, отступ больше -- внутри оного цикла.
*/

$FORWARD PrintVar, PrintEVar-B, PrintEVar-E, PrintEVar-OE,
  PrintArrVar, PrintArrEVar-B, PrintArrEVar-E,
  PrintMatchChar, PrintMatchNumber, PrintMatchName,
  PrintMatchIdent,
  PrintMatchADT, PrintMatchTerm,
  PrintMatchBrackets,
  PrintMatchEmpty,
  PrintMatchSTVar, PrintMatchRepeated,
  PrintAllocateElem, PrintReinitElem, PrintElemVar;

$FORWARD StringFromCommand, Escape;

//FROM LibraryEx
$EXTERN Dec;

PrintStkBracket {
  e.Indent s.StkBracket =
    (e.Indent
      'refalrts::Iter ' <BracketsVar-B s.StkBracket> '_stk = '
        <BracketsVar-B s.StkBracket> ';'
    )
    (e.Indent
      'refalrts::Iter ' <BracketsVar-E s.StkBracket> '_stk = '
        <BracketsVar-E s.StkBracket> ';'
    );
}

PrintRecoverStackBracket {
  e.Indent s.StkBracket =
    (e.Indent
      <BracketsVar-B s.StkBracket> ' = ' <BracketsVar-B s.StkBracket> '_stk,'
    )
    (e.Indent
      <BracketsVar-E s.StkBracket> ' = ' <BracketsVar-E s.StkBracket> '_stk,'
    );
}

BracketExcept {
  e.Brackets-B s.Except e.Brackets-E s.Except = e.Brackets-B e.Brackets-E;
}

$LABEL CCreateClosure;

PrintName {
  # CCreateClosure = 'refalrts::create_closure';

  e.Name = e.Name;
}

GenCommand {
  (e.Indent) (CmdComment e.Text) = (e.Indent) (e.Indent '//' e.Text);
  (e.Indent) (CmdPrint e.Text) = (e.Indent) (e.Indent e.Text);

  (e.Indent) (CmdDropIndent) = ('    ');

  (e.Indent) (CmdDeclareEVar e.VarInfo) =
    (e.Indent)
    (e.Indent 'static refalrts::Iter ' <PrintEVar-B e.VarInfo> ';')
    (e.Indent 'refalrts::use( ' <PrintEVar-B e.VarInfo> ' );')
    (e.Indent 'static refalrts::Iter ' <PrintEVar-E e.VarInfo> ';')
    (e.Indent 'refalrts::use( ' <PrintEVar-E e.VarInfo> ' );');

  (e.Indent) (CmdDeclareVar e.VarInfo) =
    (e.Indent)
    (e.Indent 'static refalrts::Iter ' <PrintVar e.VarInfo> ';')
    (e.Indent 'refalrts::use( ' <PrintVar e.VarInfo> ' );');

  (e.Indent) (CmdChar s.Direction s.BracketNumber s.Char) =
    (e.Indent)
    <PrintMatchChar (e.Indent) s.Direction s.BracketNumber s.Char>;

  (e.Indent) (CmdNumber s.Direction s.BracketNumber s.Number) =
    (e.Indent)
    <PrintMatchNumber (e.Indent) s.Direction s.BracketNumber s.Number>;

  (e.Indent) (CmdName s.Direction s.BracketNumber e.Name) =
    (e.Indent)
    <PrintMatchName (e.Indent) s.Direction s.BracketNumber e.Name>;

  (e.Indent) (CmdIdent s.Direction s.BracketNumber e.Name) =
    (e.Indent)
    <PrintMatchIdent (e.Indent) s.Direction s.BracketNumber e.Name>;

  (e.Indent) (CmdBrackets AlgMatch s.Left s.Right) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PosVar s.Right> ' = 0; // Right')
    <PrintMatchBrackets (e.Indent) AlgMatch s.Left s.Right>;

  (e.Indent) (CmdBrackets s.Direction s.BracketNumber s.InnerBrackets) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <BracketsVar-B s.InnerBrackets> ' = 0;')
    (e.Indent 'refalrts::Iter ' <BracketsVar-E s.InnerBrackets> ' = 0;')
    <PrintMatchBrackets (e.Indent) s.Direction s.BracketNumber s.InnerBrackets>;

  (e.Indent) (CmdADT AlgMatch s.Left s.Right) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PosVar s.Right> ' = 0; // Right')
    <PrintMatchADT (e.Indent) AlgMatch s.Left s.Right>;

  (e.Indent) (CmdADT s.Direction s.BracketNumber s.InnerBrackets e.Name) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <BracketsVar-B s.InnerBrackets> ' = 0;')
    (e.Indent 'refalrts::Iter ' <BracketsVar-E s.InnerBrackets> ' = 0;')
    <PrintMatchADT
      (e.Indent) s.Direction s.BracketNumber s.InnerBrackets e.Name
    >;

  (e.Indent) (CmdEmpty s.BracketNumber) =
    (e.Indent)
    <PrintMatchEmpty (e.Indent) s.BracketNumber>;

  (e.Indent) (CmdVar s.Direction s.BracketNumber s.Mode e.Info) =
    (e.Indent)
    <PrintMatchSTVar (e.Indent) s.Direction s.BracketNumber s.Mode e.Info>;

  (e.Indent) (CmdTerm s.Direction s.BracketNumber s.Pos) =
    (e.Indent)
    <PrintMatchTerm (e.Indent) s.Direction s.BracketNumber s.Pos>;

  (e.Indent) (CmdInner s.BracketNumber s.Left s.Right) =
    (e.Indent)
    (e.Indent
      'refalrts::Iter ' <BracketsVar-B s.BracketNumber>
      ' = ' <PosVar s.Left> ';'
    )
    (e.Indent
      'refalrts::Iter ' <BracketsVar-E s.BracketNumber>
      ' = ' <PosVar s.Right> ';'
    )
    (e.Indent
      'refalrts::move_left( ' <BracketsVar-B s.BracketNumber>
      ', ' <BracketsVar-E s.BracketNumber> ' );'
    )
    (e.Indent
      'refalrts::move_right( ' <BracketsVar-B s.BracketNumber>
      ', ' <BracketsVar-E s.BracketNumber> ' );'
    );

  (e.Indent) (CmdRepeated s.Direction s.BracketNumber e.VarInfo) =
    (e.Indent)
    <PrintMatchRepeated (e.Indent) s.Direction s.BracketNumber e.VarInfo>;

  (e.Indent) (CmdClosedE AlgLeft s.BracketNumber 'e' e.Index) =
    (e.Indent)
    (e.Indent
      <PrintEVar-B 1 'e' e.Index> ' = ' <BracketsVar-B s.BracketNumber> ';'
    )
    (e.Indent 'refalrts::use( ' <PrintEVar-B 1 'e' e.Index> ' );')
    (e.Indent
      <PrintEVar-E 1 'e' e.Index> ' = ' <BracketsVar-E s.BracketNumber> ';'
    )
    (e.Indent 'refalrts::use( ' <PrintEVar-E 1 'e' e.Index> ' );');

  (e.Indent)
  (CmdOpenedE AlgLeft s.BracketNumber 'e' e.Index (e.BracketsForSave)) =
    (e.Indent '  ')
    <Map (PrintStkBracket e.Indent) e.BracketsForSave>
    (e.Indent 'for( ')
    (e.Indent '  refalrts::Iter')
    (e.Indent
      '    ' <PrintEVar-B 1 'e' e.Index> ' = '
        <BracketsVar-B s.BracketNumber> '_stk,'
    )
    (e.Indent
      '    ' <PrintEVar-OE 1 'e' e.Index> ' = '
        <BracketsVar-B s.BracketNumber> '_stk,'
    )
    (e.Indent
      '    ' <BracketsVar-B s.BracketNumber> ' = '
        <BracketsVar-B s.BracketNumber> '_stk,'
    )
    (e.Indent
      '    ' <BracketsVar-E s.BracketNumber> ' = '
        <BracketsVar-E s.BracketNumber> '_stk;'
    )
    (e.Indent '  '
      '! empty_seq( ' <PrintEVar-OE 1 'e' e.Index> ', '
      <BracketsVar-E s.BracketNumber> ' );'
    )
    <Map (PrintRecoverStackBracket e.Indent '  ') e.BracketsForSave>
    (e.Indent '  '
      'refalrts::next_term( ' <PrintEVar-OE 1 'e' e.Index> ', '
      <BracketsVar-E s.BracketNumber> ' )'
    )
    (e.Indent ') {')
    (e.Indent '  '
      <BracketsVar-B s.BracketNumber> ' = ' <PrintEVar-OE 1 'e' e.Index> ';'
    )
    (e.Indent '  '
      <PrintEVar-B 1 'e' e.Index> ' = ' <BracketsVar-B s.BracketNumber> '_stk;'
    )
    (e.Indent '  '
      <PrintEVar-E 1 'e' e.Index> ' = ' <PrintEVar-OE 1 'e' e.Index> ';'
    )
    (e.Indent
      '  refalrts::move_right( ' <PrintEVar-B 1 'e' e.Index> ', '
      <PrintEVar-E 1 'e' e.Index> ' );'
    );

  (e.Indent) (CmdEmptyResult) =
    (e.Indent)
    ()
    (e.Indent 'refalrts::reset_allocator();')
    (e.Indent 'refalrts::Iter res = arg_begin;');

  (e.Indent) (CmdCopyEVar s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      'if( ! refalrts::copy_evar( ' <PrintEVar-B s.Usings 'e' e.Index> ', '
      <PrintEVar-E s.Usings 'e' e.Index> ', '
      <PrintEVar-B <Dec s.Usings> 'e' e.Index> ', '
      <PrintEVar-E <Dec s.Usings> 'e' e.Index>
      ' ) )'
    )
    (e.Indent '  return refalrts::cNoMemory;');

  (e.Indent) (CmdCopyVar s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      'if( ! refalrts::copy_stvar( ' <PrintVar s.Usings s.Mode e.Index> ', '
      <PrintVar <Dec s.Usings> s.Mode e.Index> ' ) )'
    )
    (e.Indent '  return refalrts::cNoMemory;');

  (e.Indent) (CmdAllocateElem s.ElemNumber e.ElemInfo) =
    (e.Indent)
    (e.Indent 'refalrts::Iter ' <PrintElemVar s.ElemNumber> ' = 0;')
    <PrintAllocateElem (e.Indent) s.ElemNumber e.ElemInfo>;

  (e.Indent) (CmdReinitElem s.ElemNumber e.ElemInfo) =
    (e.Indent)
    <PrintReinitElem (e.Indent) s.ElemNumber e.ElemInfo>;

  (e.Indent) (CmdLinkBrackets s.Left s.Right) =
    (e.Indent)
    (e.Indent
      'refalrts::link_brackets( '
      <PrintElemVar s.Left> ', ' <PrintElemVar s.Right>
      ' );'
    );

  (e.Indent) (CmdPushStack s.ElemNumber) =
    (e.Indent)
    (e.Indent 'refalrts::push_stack( ' <PrintElemVar s.ElemNumber> ' );');

  (e.Indent) (CmdInsertElem ElElem s.ElemNumber) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_elem( res, ' <PrintElemVar s.ElemNumber> ' );'
    );

  (e.Indent) (CmdInsertElem ElSeq s.Num-Begin s.Num-End) = 
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_elems( res, ' <PrintElemVar s.Num-Begin> ', ' <PrintElemVar s.Num-End> ' );'
    );

  (e.Indent) (CmdInsertElem ElSTVar s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_stvar( res, '
      <PrintVar s.Usings s.Mode e.Index> ' );'
    );

  (e.Indent) (CmdInsertEVar s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      'res = refalrts::splice_evar( res, '
      <PrintEVar-B s.Usings 'e' e.Index> ', '
      <PrintEVar-E s.Usings 'e' e.Index> ' );'
    );

  (e.Indent) (CmdReturnResult) =
    ('    ')
    (e.Indent 'refalrts::use( res );')
    (e.Indent 'refalrts::splice_to_freelist( arg_begin, arg_end );')
    (e.Indent 'return refalrts::cSuccess;')
    <GenCloseBrackets e.Indent>;

  (e.Indent) (CmdIfDef e.Macros) =
    (e.Indent) ('#ifdef ' e.Macros);

  (e.Indent) (CmdElse) = 
    (e.Indent) ('#else');

  (e.Indent) (CmdEndIf) = 
    (e.Indent) ('#endif');

  (e.Indent) (CmdInitRAA) = 
    (e.Indent '  ')
    (e.Indent 'const static refalrts::ResultAction raa[] = {');

  (e.Indent '  ') (CmdFinRAA) = 
    (e.Indent)
    (e.Indent '  {refalrts::icEnd}')
    (e.Indent '};')
    (e.Indent
      'refalrts::Iter allocs[2*sizeof(raa)/sizeof(raa[0])];'
    )
    (e.Indent
      'refalrts::FnResult res = '
      'refalrts::interpret_array( raa, allocs, arg_begin, arg_end );'
    )
    (e.Indent 'return res;')
    <GenCloseBrackets e.Indent>;

  (e.Indent) (CmdArrChar s.Value) =
    (e.Indent)
    (e.Indent '{refalrts::icChar, 0, 0, \'' <Escape s.Value> '\'},');

  (e.Indent) (CmdArrFunc e.Name) =
    (e.Indent)
    (e.Indent
      '{refalrts::icFunc, '
      '(void*) & ' <PrintName e.Name> ', '
      '(void*) "' <PrintName e.Name> '"},'
    );

  (e.Indent) (CmdArrInt s.Number) =
    (e.Indent)
    (e.Indent '{refalrts::icInt, 0, 0, ' s.Number '},');

  (e.Indent) (CmdArrIdent e.Name) =
    (e.Indent)
    (e.Indent '{refalrts::icIdent, (void*) & ' e.Name 'L_<int>::name},');

  (e.Indent) (CmdArrOB) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibOpenBracket},');

  (e.Indent) (CmdArrCB) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibCloseBracket},');

  (e.Indent) (CmdArrOADT) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibOpenADT},');

  (e.Indent) (CmdArrCADT) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibCloseADT},');

  (e.Indent) (CmdArrOC) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibOpenCall},');

  (e.Indent) (CmdArrCC) =
    (e.Indent)
    (e.Indent '{refalrts::icBracket, 0, 0, refalrts::ibCloseCall},');

  (e.Indent) (CmdArrCopy 'e' e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icCopyEVar, '
      '& ' <PrintArrEVar-B 'e' e.Index> ', '
      '& ' <PrintArrEVar-E 'e' e.Index> '},'
    );

  (e.Indent) (CmdArrCopy s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icCopySTVar, & ' <PrintArrVar s.Mode e.Index> '},'
    );

  (e.Indent) (CmdArrSplice s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icSpliceEVar, '
      '& ' <PrintEVar-B s.Usings 'e' e.Index> ', '
      '& ' <PrintEVar-E s.Usings 'e' e.Index> '},'
    );

  (e.Indent) (CmdArrSplice s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent
      '{refalrts::icSpliceSTVar, & ' <PrintVar s.Usings s.Mode e.Index> '},'
    );
}

PrintVar {
  s.Usings s.Mode e.Index = s.Mode e.Index '_' <StrFromInt s.Usings>;
}

PrintArrVar {
  s.Mode e.Index = s.Mode e.Index '_1';
}

PrintEVar-B {
  s.Usings s.Mode e.Index = s.Mode e.Index '_b_' <StrFromInt s.Usings>;
}

PrintArrEVar-B {
  s.Mode e.Index = s.Mode e.Index '_b_1';
}

PrintEVar-E {
  s.Usings s.Mode e.Index = s.Mode e.Index '_e_' <StrFromInt s.Usings>;
}

PrintArrEVar-E {
  s.Mode e.Index = s.Mode e.Index '_e_1';
}

PrintEVar-OE {
  s.Usings s.Mode e.Index = s.Mode e.Index '_oe_' <StrFromInt s.Usings>;
}

$FORWARD PrintMatch;

Escape {
  '\n' = '\\n';
  '\t' = '\\t';
  '\r' = '\\r';
  '\\' = '\\\\';
  '' = '\\' '';
  s.Other = s.Other;
}

PrintMatchChar {
  (e.Indent) s.Direction s.BracketNumber s.Char =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('char') '' <Escape s.Char> ''
    >;
}

PrintMatchNumber {
  (e.Indent) s.Direction s.BracketNumber s.Number =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('number') <StrFromInt s.Number> 'UL'
    >;
}

PrintMatchName {
  (e.Indent) s.Direction s.BracketNumber e.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('function') '& ' e.Name
    >;
}

PrintMatchIdent {
  (e.Indent) s.Direction s.BracketNumber e.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('ident') ' & ' e.Name 'L_<int>::name'
    >;
}

PrintMatchBrackets {
  (e.Indent) AlgMatch s.Left s.Right =
    <PrintMatch
      (e.Indent) AlgMatch s.Left ('brackets') <PosVar s.Right>
    >;

  (e.Indent) s.Direction s.BracketNumber s.InnerNum =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('brackets')
      <BracketsVar-B s.InnerNum> ', ' <BracketsVar-E s.InnerNum>
    >;
}

PrintMatchADT {
  (e.Indent) AlgMatch s.Left s.Right e.Name =
    <PrintMatch
      (e.Indent) AlgMatch s.Left ('adt') <PosVar s.Right>
    >;

  (e.Indent) s.Direction s.BracketNumber s.InnerNum e.Name =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('adt')
      <BracketsVar-B s.InnerNum> ', '
      <BracketsVar-E s.InnerNum> ', '
      '& ' e.Name
    >;
}

BreakerFromIndent {
  '    ' = 'break;';

  e.OtherIndent = 'continue;';
}

PrintMatchEmpty {
  (e.Indent) s.BracketNumber =
    (e.Indent
      'if( ! empty_seq( ' <BracketsVar-B s.BracketNumber> ', '
      <BracketsVar-E s.BracketNumber> ' ) )'
    )
    (e.Indent '  ' <BreakerFromIndent e.Indent>);
}

$LABEL Dir, Num, NoNum, DirLeft, DirRight;

StrFromDirection {
  s.Direction =
    <Fetch
      <s.Direction #Dir>
      {
        #DirLeft = 'left';
        #DirRight = 'right';
      }
    >;
}

VarMatchFunction { 's' = 'svar'; 't' = 'tvar'; }

PrintMatchSTVar {
  (e.Indent) s.Direction s.BracketNumber s.Mode e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      (<VarMatchFunction s.Mode>) <PrintVar 1 s.Mode e.Index>
    >;
}

PrintMatchTerm {
  (e.Indent) s.Direction s.BracketNumber s.Pos =
    (e.Indent 'refalrts::Iter ' <PosVar s.Pos> ' = 0;')
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('tvar') <PosVar s.Pos>
    >;
}

PrintMatchRepeated {
  (e.Indent) s.Direction s.BracketNumber s.Usings 'e' e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('repeated_evar')
      <PrintEVar-B s.Usings 'e' e.Index> ', '
      <PrintEVar-E s.Usings 'e' e.Index> ', '
      <PrintEVar-B <Dec s.Usings> 'e' e.Index> ', '
      <PrintEVar-E <Dec s.Usings> 'e' e.Index>
    >;

  (e.Indent) s.Direction s.BracketNumber s.Usings s.Mode e.Index =
    <PrintMatch
      (e.Indent) s.Direction s.BracketNumber
      ('repeated_stvar') <PrintVar s.Usings s.Mode e.Index>
      ', ' <PrintVar <Dec s.Usings> s.Mode e.Index>
    >;
}

PrintMatch {
  (e.Indent) AlgMatch s.Pos (e.Function) e.TextRep =
    (e.Indent
      'if( ! refalrts::' e.Function '_match'
      '( ' e.TextRep ', ' <PosVar s.Pos> ' ) ) '
    )
    (e.Indent '  ' <BreakerFromIndent e.Indent>);

  (e.Indent) s.Direction s.BracketNumber (e.Function) e.TextRep =
    <Fetch
      <s.Direction #Num>
      {
        #NoNum =
          (e.Indent
            'if( ! refalrts::' e.Function '_' <StrFromDirection s.Direction>
            '( ' e.TextRep ', ' <BracketsVar-B s.BracketNumber> ', '
          <BracketsVar-E s.BracketNumber> ' ) ) '
          )
          (e.Indent '  ' <BreakerFromIndent e.Indent>);

        s.Num =
          (e.Indent 'refalrts::Iter ' <PrintElemVar s.Num> ';')
          (e.Indent
            'if( (' <PrintElemVar s.Num>
            ' = refalrts::' e.Function '_' <StrFromDirection s.Direction>
            '( ' e.TextRep ', ' <BracketsVar-B s.BracketNumber> ', '
          <BracketsVar-E s.BracketNumber> ' )) == 0 ) '
          )
          (e.Indent '  ' <BreakerFromIndent e.Indent>)
          (e.Indent 'refalrts::use( ' <PrintElemVar s.Num> ' );');
      }
    >;
}

PrintElemVar {
  s.Number = 'n' <StrFromInt s.Number>;
}

$FORWARD PrintAllocate-Aux, SwAllocator, SwInfo;

PrintAllocateElem {
  (e.Indent) s.ElemNumber s.ElementType e.Info =
    <PrintAllocate-Aux
      (e.Indent) s.ElemNumber
      ( <SwAllocator s.ElementType> )
      ( <SwInfo s.ElementType e.Info> )
    >
    (e.Indent '  return refalrts::cNoMemory;');
}

PrintAllocate-Aux {
  (e.Indent) s.ElemNumber (e.Constructor) (e.Value) =
    (e.Indent
      'if( ! refalrts::' e.Constructor '( ' <PrintElemVar s.ElemNumber>
      e.Value ' ) )'
    );
}

$FORWARD SwElementFunc;

PrintReinitElem {
  (e.Indent) s.ElemNumber s.ElementType e.Info =
    (e.Indent
      'refalrts::reinit_' <SwElementFunc s.ElementType>
      '( ' <PrintElemVar s.ElemNumber> <SwInfo s.ElementType e.Info> ' );'
    );
}

SwAllocator {
  s.Element = 'alloc_' <SwElementFunc s.Element>;
}

SwElementFunc {
  ElChar = 'char';
  ElName = 'name';
  ElNumber = 'number';
  ElIdent = 'ident';

  ElOpenADT = 'open_adt';
  ElCloseADT = 'close_adt';

  ElOpenBracket = 'open_bracket';
  ElCloseBracket = 'close_bracket';

  ElOpenCall = 'open_call';
  ElCloseCall = 'close_call';
}

SwInfo {
  ElChar s.Char = ', ' '' <Escape s.Char> '';

  ElName e.Name = ', & ' <PrintName e.Name> ', "' <PrintName e.Name> '"';

  ElNumber s.Number = ', ' <StrFromInt s.Number> 'UL';

  ElIdent e.Name = ', & ' e.Name 'L_<int>::name';

  // Остальное -- скобки
  s.Other = ;
}

GenCloseBrackets {
  '    ' = ('  } while ( 0 );');

  '  ' e.Indent =
    (e.Indent '}') <GenCloseBrackets e.Indent>;
}

ElemVarName {
  s.Num = 'h' <StrFromInt s.Num>;
}

InsertElem {
  ElElem s.Number = <ElemVarName s.Number>;

  ElSTVar s.Usings s.Mode e.Index =
    <PrintVar s.Usings s.Mode e.Index>;
}

//FROM LibraryEx
$EXTERN Dec;

SwStringFromCommand {
  CmdReturnResult = 'RETURN res';
}

StringFromCommand {
  (s.Command e.Arguments) =
    ( '    // ' <SwStringFromCommand s.Command e.Arguments> );
}
