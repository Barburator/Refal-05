//FROM Library
$EXTERN Success, Fails;

//FROM Lexer
$EXTERN LexFolding;

//FROM LibraryEx
$EXTERN Map, Fetch, Seq, SaveFile;


//FROM SymTable
$EXTERN ST-Create, ST-CheckUnresolved, ST-Destroy;

//FROM Error
$EXTERN EL-Create, EL-Destroy, EL-HasErrors, EL-NoErrors;

/**
  <CompileFile (e.SrcName) e.OutputName>
    == Success e.OutputName
    == Fails
*/
$ENTRY CompileFile {
  (e.SrcName) e.OutputName =
    <Fetch
      <LexFolding e.SrcName>
      <Seq
        (Map PrepareBracket)
        {
          e.LexFolding =
            <ParseElements
              <EL-Create e.SrcName> <ST-Create> (/* generated */)
              e.LexFolding
            >;
        }
        {
          t.ErrorList t.SymTable e.Lines =
            <ST-CheckUnresolved t.ErrorList t.SymTable> e.Lines;
        }
        {
          t.ErrorList t.SymTable e.Lines =
            <ST-Destroy t.SymTable>
            <Fetch
              <EL-Destroy t.ErrorList>
              {
                EL-NoErrors =
                  <SaveFile (e.OutputName) e.Lines>
                  Success e.OutputName;

                EL-HasErrors =
                  Fails;
              }
            >;
        }
      >
    >;
}

//FROM Lexer
$EXTERN TkOpenBracket, TkCloseBracket, TkOpenADT, TkCloseADT,
  TkOpenCall, TkCloseCall;

$LABEL COpen, CClose;

PrepareBracket {
  (TkOpenBracket t.SrcPos) = (# COpen t.SrcPos TkOpenBracket);
  (TkCloseBracket t.SrcPos) = (# CClose t.SrcPos TkCloseBracket);

  (TkOpenADT t.SrcPos) = (# COpen t.SrcPos TkOpenADT);
  (TkCloseADT t.SrcPos) = (# CClose t.SrcPos TkCloseADT);

  (TkOpenCall t.SrcPos) = (# COpen t.SrcPos TkOpenCall);
  (TkCloseCall t.SrcPos) = (# CClose t.SrcPos TkCloseCall);

  t.OtherToken = t.OtherToken;
}

/*
  Трансляция осуществляется обычным методом рекурсивного спуска.

  TranslationUnit = Element* .

  Element =
    '$ENUM' NameList |
    '$EENUM' NameList |
    '$EXTERN' NameList |
    '$LABEL' NameList |
    '$ENTRY' Function |
    Function .

  NameList = Name ',' NameList | Name ';' .

  Function = Name Block .

  Block = '{' Sentence* '}' .

  Sentence = Pattern '=' Result ';' .

  Pattern = PatternTerm* .

  PatternTerm = CommonTerm |
    '(' Patter ')' | '[' Pattern ']' | RedefinitionVariable .

  RedefinitionVariable = Variable '^' .

  CommonTerm = Char | Number | Name | Variable | '#' Ident .

  Result = ResultTerm* .

  ResultTerm = CommonTerm |
    '(' Result ')' | '[' Result ']' | '<' Result '>' | Block .

  Стратегия обработки ошибок. Выделяем только одну функцию, проглатывающую
  неожиданные лексемы. Остальные функции при обнаружении неожиданной лексемы
  должны её оставить в лексической свёртке перейдя в одно из состояний уровнем
  выше. Данная стратегия обработки ошибок может нарушаться, но при этом должно
  выполняться условие, что TkEOF передаётся в ParseElements.
*/

/*
  TranslationUnit = Element* .
*/

/*
  Element =
    '$ENUM' NameList |
    '$EENUM' NameList |
    '$EXTERN' NameList |
    '$LABEL' NameList |
    '$ENTRY' Function |
    Function .
*/

//FROM SymTable
$EXTERN ST-AllFunctions, ST-AllIdents,
  ST-AddDeclared, ST-AddDefined, ST-AddIdent;

//FROM Error
$EXTERN EL-AddError, EL-AddErrorAt, EL-AddUnexpected;

//FROM Lexer
$EXTERN
  TkDirective,
  Enum, EEnum, Extern, Entry, Ident,
  TkName, TkEOF, TkSemicolon;

//FROM Generator
$EXTERN GN-Entry, GN-Local, GenCommonHeaders, GenCommonTailer,
  GenExtern, GenForward;

//FROM LibraryEx
$EXTERN Sort;

ParseElements {
  t.ErrorList t.SymTable (e.Generated) (TkEOF t.SrcPos) =
    t.ErrorList t.SymTable
    <GenCommonHeaders>
    <Map
      {
        (GN-Entry e.Name) = <GenExtern e.Name>;
        (GN-Local e.Name) = <GenForward e.Name>;
      }
      <Sort <ST-AllFunctions t.SymTable>>
    >
    ()
    <Map
      { (e.Name) = <GenIdent e.Name>; }
      <Sort <ST-AllIdents t.SymTable>>
    >
    e.Generated
    <GenCommonTailer>;

  t.ErrorList t.SymTable (e.Generated)
  (TkDirective t.SrcPos s.Directive) e.Tail =
    <Fetch
      <ParseElement-SwDirective
        t.ErrorList t.SymTable s.Directive e.Tail
      >
      {
        (e.NewGenerated) t.ErrorList^ t.SymTable^ e.Tail^ =
          <ParseElements
            t.ErrorList t.SymTable (e.Generated e.NewGenerated) e.Tail
          >;
      }
    >;

  t.ErrorList t.SymTable (e.Generated) (TkName t.SrcPos e.Name) e.Tail =
    <Fetch
      <ParseFunction
        GN-Local (e.Name)
        <ST-AddDefined t.ErrorList t.SymTable GN-Local t.SrcPos e.Name>
        e.Tail
      >
      {
        (e.NewGenerated) t.ErrorList^ t.SymTable^ e.Tail^ =
          <ParseElements
            t.ErrorList t.SymTable (e.Generated e.NewGenerated) e.Tail
          >;
      }
    >;

  t.ErrorList t.SymTable (e.Generated) (TkSemicolon t.SrcPos) e.Tail =
    <ParseElements t.ErrorList t.SymTable (e.Generated) e.Tail>;

  t.ErrorList t.SymTable (e.Generated) t.Unexpected e.Tail =
    <ParseElements
      <EL-AddUnexpected t.ErrorList t.Unexpected 'directive or name'>
      t.SymTable (e.Generated) e.Tail
    >;
}

//FROM Generator
$EXTERN GenEEnum, GenEnum, GenExtern, GenForward, GenIdent;

DontGen {
  e.Name = /* пусто */;
}

GenFunctionsFromDirective {
  EEnum   = GenEEnum   ST-AddDefined  GN-Entry;
  Enum    = GenEnum    ST-AddDefined  GN-Local;
  Extern  = DontGen    AddExtern      GN-Entry;
  // Для меток не важно: entry или local.
  Ident   =
    DontGen
    {
      t.ErrorList t.SymTable s.ScopeClass t.SrcPos e.Name =
        t.ErrorList <ST-AddIdent t.SymTable e.Name>;
    }
    GN-Entry;
}

AddExtern {
  t.ErrorList t.SymTable s.ScopeClass t.SrcPos e.Name =
    t.ErrorList <ST-AddDeclared t.SymTable e.Name>;
}

ParseElement-SwDirective {
  t.ErrorList t.SymTable Entry (TkName t.SrcPos e.Name) e.Tail =
    <ParseFunction
      GN-Entry (e.Name)
      <ST-AddDefined t.ErrorList t.SymTable GN-Entry t.SrcPos e.Name>
      e.Tail
    >;

  t.ErrorList t.SymTable Entry t.Unexpected e.Tail =
    (/* ничего не генерируем */)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'function name'>
    t.SymTable t.Unexpected e.Tail;

  t.ErrorList t.SymTable s.Directive e.Tail =
    <ParseNameList
      (/* generated */)
      t.ErrorList t.SymTable
      <GenFunctionsFromDirective s.Directive>
      e.Tail
    >;
}

/*
  NameList = Name ',' NameList | Name ';' .
*/

//FROM Lexer
$EXTERN TkComma, TkSemicolon;

ParseNameList {
  (e.Generated)
  t.ErrorList t.SymTable s.GenFunc s.TableChangeFunc s.ScopeClass
  (TkName t.SrcPos e.Name) e.Tail =
    <ParseNameList-Tail
      (e.Generated <s.GenFunc e.Name>)
      <s.TableChangeFunc t.ErrorList t.SymTable s.ScopeClass t.SrcPos e.Name>
      s.GenFunc s.TableChangeFunc s.ScopeClass
      e.Tail
    >;

  (e.Generated)
  t.ErrorList t.SymTable s.GenFunc s.TableChangeFunc s.ScopeClass
  t.Unexpected e.Tail =
    (e.Generated)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'function name'>
    t.SymTable t.Unexpected e.Tail;
}

ParseNameList-Tail {
  (e.Generated)
  t.ErrorList t.SymTable s.GenFunc s.TableChangeFunc s.ScopeClass
  (TkComma t.SrcPos) e.Tail =
    <ParseNameList
      (e.Generated)
      t.ErrorList t.SymTable s.GenFunc s.TableChangeFunc s.ScopeClass e.Tail
    >;

  (e.Generated)
  t.ErrorList t.SymTable s.GenFunc s.TableChangeFunc s.ScopeClass
  (TkSemicolon t.SrcPos) e.Tail =
    (e.Generated) t.ErrorList t.SymTable e.Tail;

  (e.Generated)
  t.ErrorList t.SymTable s.GenFunc s.TableChangeFunc s.ScopeClass
  t.Unexpected e.Tail =
    (e.Generated)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'comma or semicolon'>
    t.SymTable t.Unexpected e.Tail;
}

/*
  Function = Name '{' Sentence* '}'
*/

//FROM Lexer
$EXTERN TkOpenBlock, TkCloseBlock;

//FROM Generator
$EXTERN GenFnStart, GenSentence, GenFnEnd, GenFnEnd-Success;

//FROM Context
$EXTERN Cntx-Create, Cntx-Destroy;

//FROM Library
$EXTERN StrFromInt;

CreateLambdaName {
  e.Name s.Number =
    'lambda_' e.Name '_' <StrFromInt s.Number>;
}

$LABEL CLambdaScope;

//FROM Algorithm
$EXTERN MakeAlgorithm;

//FROM Lexer
$EXTERN TkName;

$LABEL CLambdaName;

ParseFunction {
  s.ScopeClass (e.Name) t.ErrorList t.SymTable
  (TkOpenBlock t.SrcPos) e.Tail =
    <Fetch
      <ParseBlock
        (0) t.ErrorList t.SymTable <Cntx-Create> e.Tail
      >
      {
        (s.NextNumber e.FunctionBodies t.MainBody)
        t.ErrorList^ t.SymTable^ (t.Context) e.Tail^ =
          <Cntx-Destroy t.Context>
          (
            <Map
              {
                (s.Number e.Sentences) =
                  <GenFunctionBody
                    GN-Local (e.Name) (<CreateLambdaName e.Name s.Number>)
                    e.Sentences
                  >;
              }
              e.FunctionBodies
            >
            <Fetch
              t.MainBody
              {
                (s.Number e.Sentences) =
                  <GenFunctionBody
                    s.ScopeClass (e.Name) (e.Name) e.Sentences
                  >;
              }
            >
          )
          t.ErrorList t.SymTable e.Tail;
      }
    >;

  s.ScopeClass (e.Name) t.ErrorList t.SymTable
  t.Unexpected e.Tail =
    (/* ничего не сгенерировали */)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'open brace'>
    t.SymTable t.Unexpected e.Tail;
}

$LABEL Brackets, ADT-Brackets, CallBrackets;

ResolveLambdaName {
  (e.MainName) e.Result =
    <Map
      {
        (TkName #CLambdaName s.Number) =
          (TkName <CreateLambdaName e.MainName s.Number>);

        (#Brackets e.SubResult) =
          (#Brackets <ResolveLambdaName (e.MainName) e.SubResult>);

        (#ADT-Brackets (e.Name) e.SubResult) =
          (#ADT-Brackets (e.Name) <ResolveLambdaName (e.MainName) e.SubResult>);

        (#CallBrackets e.SubResult) =
          (#CallBrackets <ResolveLambdaName (e.MainName) e.SubResult>);

        t.OtherTerm = t.OtherTerm;
      }
      e.Result
    >;
}

GenFunctionBody {
  // Закомментированный код содержит вариант кодогенерации,
  // при котором если последнее предложение имеет вид
  // e.X = ...;, в конце не генерится возврат
  // return RecognitionImpossible;

  //s.ScopeClass (e.MainName) (e.Name)
  //e.Sentences (((TkVariable 'e' e.Index)) (e.Result)) =
  //  <GenFnStart s.ScopeClass e.Name>
  //  <Map
  //    <PrepareAndGenSentence e.MainName>
  //    e.Sentences
  //    (((TkVariable 'e' e.Index)) (e.Result))
  //  >
  //  <GenFnEnd-Success>;

  s.ScopeClass (e.MainName) (e.Name) e.Sentences =
    <GenFnStart s.ScopeClass e.Name>
    <Fetch
      e.Sentences
      (Map {
        ((e.Pattern) (e.Result)) =
          <GenSentence
            <MakeAlgorithm
              (e.Pattern) (<ResolveLambdaName (e.MainName) e.Result>)
            >
          >;
      })
    >
    <GenFnEnd>;
}

//FROM Lexer
$EXTERN TkVariable;

//FROM Context
$EXTERN Cntx-PushScope, Cntx-PopScope;

/*
  <ParseBlock
    (e.FunctionBodies) t.ErrorList t.SymTable t.Context e.Tokens
  >
    == (e.FunctionBodies)
    t.ErrorList t.SymTable (t.Context e.FreeVars) e.Tokens

  e.FunctionBodies ::= s.NextNumber t.FunctionBody*
  t.FunctionBody ::= (s.Number ((e.Pattern) (e.Result))*)

  Открывающая скобка должна отсутствовать во входном потоке.
*/
ParseBlock {
  (e.Functions)
  t.ErrorList t.SymTable t.Context e.Tokens =
    <DoParseBlock
      (e.Functions) t.ErrorList t.SymTable
      <Cntx-PushScope t.Context>
      (/* sentences */) e.Tokens
    >;
}

//FROM LibraryEx
$EXTERN Inc, Dec;

DoParseBlock {
  (s.Number e.Functions)
  t.ErrorList t.SymTable t.Context
  (e.Sentences)
  (TkCloseBlock t.SrcPos) e.Tail =
    <Fetch
      <Cntx-PopScope t.Context>
      {
        t.Context^ e.FreeContext =
          (
            <Inc s.Number> e.Functions
            (
              s.Number
              <Map
                (AddContextToSentence e.FreeContext)
                e.Sentences
              >
            )
          )
          t.ErrorList t.SymTable (t.Context e.FreeContext);
      }
    >
    e.Tail;

  (s.Number e.Functions)
  t.ErrorList t.SymTable t.Context
  (e.Sentences) (TkEOF t.SrcPos) e.Tail =
    (
      <Inc s.Number>
      e.Functions (s.Number e.Sentences)
    )
    <EL-AddErrorAt
      t.ErrorList t.SrcPos 'Unexpected EOF, expected "}"'
    >
    t.SymTable
    ( <Cntx-PopScope t.Context> )
    (TkEOF t.SrcPos) e.Tail;

  (e.Functions)
  t.ErrorList t.SymTable t.Context
  (e.Sentences) e.Tokens =
    <DoParseBlock
      <ParseSentence
        (e.Functions) t.ErrorList t.SymTable t.Context
        (e.Sentences) e.Tokens
      >
    >;
}

AddContextToSentence {
  e.Context ((e.Pattern) (e.Result)) =
    (
      (
        <CreateContext e.Context>
        e.Pattern
      )
      (e.Result)
    );
}

CreateContext {
  e.Context =
    <Map
      {
        ('e' e.Index) = (#Brackets (TkVariable 'e' e.Index));
        (s.Mode e.Index) = (TkVariable s.Mode e.Index);
      }
      e.Context
    >;
}

//FROM LibraryEx
$EXTERN Seq;

//FROM Lexer
$EXTERN TkReplace;

//FROM Context
$EXTERN Cntx-ResetAfterSentence;

ParseSentence {
  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.Sentences) e.Tokens =
    <Fetch
      <ParsePattern
        t.ErrorList t.SymTable t.Context ( /* мультискобки */ )
        ( /* просканировано */ ) e.Tokens
      >
      <Seq
        {
          t.ErrorList^ t.SymTable^ t.Context^
          (e.Pattern) (TkReplace t.SrcPos) e.Tokens^ =
            t.ErrorList t.SymTable t.Context
            (e.Pattern) e.Tokens;

          t.ErrorList^ t.SymTable^ t.Context^
          (e.Pattern) (s.NextResultTerm t.SrcPos e.Info) e.Tokens^ =
            <EL-AddErrorAt
              t.ErrorList t.SrcPos 'Incorrect pattern termination'
            >
            t.SymTable t.Context
            (e.Pattern) e.Tokens;
        }
        {
          t.ErrorList^ t.SymTable^ t.Context^
          (e.Pattern) e.Tokens^ =
            (e.Pattern)
            <ParseResult
              (e.Functions) t.ErrorList t.SymTable t.Context
              ( /* мультискобки */ ) ( /* просканировано */ ) e.Tokens
            >;
        }
        {
          (e.Pattern)
          (e.Functions^) t.ErrorList^ t.SymTable^ t.Context^
          (e.Result) (TkSemicolon t.SrcPos) e.Tokens^ =
            (e.Functions) t.ErrorList t.SymTable
            <Cntx-ResetAfterSentence t.Context>
            (e.Sentences ((e.Pattern) (e.Result))) e.Tokens;

          (e.Pattern)
          (e.Functions^) t.ErrorList^ t.SymTable^ t.Context^
          (e.Result) (TkCloseBlock t.SrcPos) e.Tokens^ =
            (e.Functions)
            <EL-AddErrorAt t.ErrorList t.SrcPos 'Missed semicolon'>
            t.SymTable
            <Cntx-ResetAfterSentence t.Context>
            (e.Sentences ((e.Pattern) (e.Result)))
            (TkCloseBlock t.SrcPos) e.Tokens;

          (e.Pattern)
          (e.Functions^) t.ErrorList^ t.SymTable^ t.Context^
          (e.Result) t.Unexpected e.Tokens^ =
            (e.Functions)
            <EL-AddUnexpected t.ErrorList t.Unexpected 'semicolon'>
            t.SymTable
            <Cntx-ResetAfterSentence t.Context>
            (e.Sentences ((e.Pattern) (e.Result)))
            t.Unexpected e.Tokens;
        }
      >
    >;
}


//FROM Lexer
$EXTERN TkChar, TkNumber, TkVariable, TkIdentMarker,
  TkOpenBracket, TkCloseBracket, TkOpenADT, TkCloseADT,
  TkRedefinition, TkOpenCall, TkCloseCall;

//FROM Algorithm
$EXTERN TkIdentifier;

//FROM SymTable
$EXTERN ST-AddFunctionCall, ST-GetAnyName;

//FROM Context
$EXTERN Cntx-AddNewVariable, Cntx-AddVariable;

$LABEL CSuccess, CInvalidMode, CAlreadyBounded;

ParsePattern {
  // Обработка литералов атомов

  t.ErrorList t.SymTable t.Context (e.MultiBrackets)
  (e.Scanned) (TkChar t.SrcPos s.Char) e.Tail =
    <ParsePattern
      t.ErrorList t.SymTable t.Context (e.MultiBrackets)
      (e.Scanned (TkChar s.Char)) e.Tail
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBrackets)
  (e.Scanned) (TkNumber t.SrcPos s.Number) e.Tail =
    <ParsePattern
      t.ErrorList t.SymTable t.Context (e.MultiBrackets)
      (e.Scanned (TkNumber s.Number)) e.Tail
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBrackets)
  (e.Scanned) (TkName t.SrcPos e.Name) e.Tail =
    <ParsePattern
      t.ErrorList <ST-AddFunctionCall t.SymTable t.SrcPos e.Name>
      t.Context (e.MultiBrackets)
      (e.Scanned (TkName e.Name)) e.Tail
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBrackets)
  (e.Scanned)
  (TkIdentMarker t.SrcPosMarker) (TkName t.SrcPosName e.Name) e.Tail =
    <ParsePattern
      t.ErrorList <ST-AddIdent t.SymTable e.Name>
      t.Context (e.MultiBrackets)
      (e.Scanned (TkIdentifier e.Name)) e.Tail
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBrackets)
  (e.Scanned) (TkIdentMarker t.SrcPos) e.Tail =
    <ParsePattern
      <EL-AddErrorAt t.ErrorList t.SrcPos 'Missed name after ''#'''>
      t.SymTable t.Context (e.MultiBrackets)
      (e.Scanned) e.Tail
    >;

  // Переменные

  t.ErrorList t.SymTable t.Context (e.MultiBracket)
  (e.Scanned)
  (TkVariable t.SrcPosVar s.Mode e.Index)
  (TkRedefinition t.SrcPosRedef)
  e.Tail =
    <Fetch
      <CheckAddVariable
        t.ErrorList t.Context
        Cntx-AddNewVariable t.SrcPosVar s.Mode e.Index
      >
      {
        t.ErrorList^ t.Context^ s.Mode^ s.Depth =
          <ParsePattern
            t.ErrorList t.SymTable t.Context (e.MultiBracket)
            (e.Scanned (TkVariable s.Mode e.Index s.Depth)) e.Tail
          >;
      }
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBracket)
  (e.Scanned) (TkVariable t.SrcPos s.Mode e.Index) e.Tail =
    <Fetch
      <CheckAddVariable
        t.ErrorList t.Context
        Cntx-AddVariable t.SrcPos s.Mode e.Index
      >
      {
        t.ErrorList^ t.Context^ s.Mode^ s.Depth =
          <ParsePattern
            t.ErrorList t.SymTable t.Context (e.MultiBracket)
            (e.Scanned (TkVariable s.Mode e.Index s.Depth)) e.Tail
          >;
      }
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBracket)
  (e.Scanned) (TkRedefinition t.SrcPos) e.Tail =
    <ParsePattern
      <EL-AddErrorAt t.ErrorList t.SrcPos 'Naked redefinition sign'>
      t.SymTable t.Context (e.MultiBracket)
      (e.Scanned) e.Tail
    >;

  /*
    Скобки.

    Стратегия обработки ошибок: если мы встретили "не ту" закрывающую
    скобку, считаем, что "та" была пропущена.
    Если имеем непарную открывающую скобку, считаем,
    что соответствующая скобка была пропущена в конце выражения;
    если имеем непарную закрывающую скобку, считаем её лишней.
  */

  t.ErrorList t.SymTable t.Context (e.MultiBracket)
  (e.Scanned) (# COpen t.SrcPos TkOpenBracket) e.Tail =
    <ParsePattern
      t.ErrorList t.SymTable t.Context
      (
        e.MultiBracket
        (#Brackets t.SrcPos TkCloseBracket e.Scanned)
      )
      () e.Tail
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBracket)
  (e.Scanned)
  (# COpen t.SrcPosBracket TkOpenADT) (TkName t.SrcPosName e.Name) e.Tail =
    <ParsePattern
      t.ErrorList <ST-AddFunctionCall t.SymTable t.SrcPosName e.Name>
      t.Context
      (
        e.MultiBracket
        (#ADT-Brackets t.SrcPosBracket TkCloseADT e.Scanned)
      )
      ((e.Name)) e.Tail
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBracket)
  (e.Scanned)
  (# COpen t.SrcPos TkOpenADT) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList t.SrcPos 'Expected name after "["'
      >
      t.SymTable t.Context
      (
        e.MultiBracket
        (#ADT-Brackets t.SrcPos TkCloseADT e.Scanned)
      )
      ((<ST-GetAnyName t.SymTable>)) e.Tail
    >;

  t.ErrorList t.SymTable t.Context (e.MultiBracket)
  (e.Scanned)
  (# COpen t.SrcPos TkOpenCall) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList t.SrcPos 'Unexpected call bracket in pattern expression'
      >
      t.SymTable t.Context
      (
        e.MultiBracket
        (#Brackets t.SrcPos TkCloseCall e.Scanned)
      )
      () e.Tail
    >;

  t.ErrorList t.SymTable t.Context
  (
    e.MultiBracket
    (s.BracketsSign t.InnerSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (# CClose t.CloseSrcPos s.CloseBracket) e.Tail =
    <ParsePattern
      t.ErrorList t.SymTable t.Context (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList t.SymTable t.Context
  (
    e.MultiBracket
    (s.BracketsSign t.InnerSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (# CClose t.SrcPos s.OtherCloseBracket) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList t.InnerSrcPos
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      t.SymTable t.Context (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      (# CClose t.SrcPos s.OtherCloseBracket) e.Tail
    >;

  t.ErrorList t.SymTable t.Context ()
  (e.Scanned) (# CClose t.SrcPos s.CloseBracket) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList t.SrcPos
        'Unbalanced bracket "' <StrFromBracket s.CloseBracket> '"'
      >
      t.SymTable t.Context ()
      (e.Scanned) e.Tail
    >;

  t.ErrorList t.SymTable t.Context
  (
    e.MultiBracket
    (s.BracketsSign t.InnerSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) e.Tail =
    <ParsePattern
      <EL-AddErrorAt
        t.ErrorList t.InnerSrcPos
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      t.SymTable t.Context (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList t.SymTable t.Context ()
  (e.Pattern) e.Tail =
    t.ErrorList t.SymTable t.Context
    (e.Pattern)  e.Tail;
}

InvalidModeVariableError {
  t.ErrorList t.SrcPos s.OldMode s.NewMode e.Index =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos
      'Variables ' s.OldMode '.' e.Index ' and '
      s.NewMode '.' e.Index ' has different modes'
    >;
}

CheckAddVariable {
  t.ErrorList t.Context
  s.FnContextModifier t.SrcPos s.Mode e.Index =
    <Fetch
      <s.FnContextModifier t.Context s.Mode e.Index>
      {
        t.Context^ # CSuccess s.Depth =
          t.ErrorList t.Context s.Mode s.Depth;

        t.Context^ # CInvalidMode s.Depth s.OldMode =
          <InvalidModeVariableError
            t.ErrorList t.SrcPos s.OldMode s.Mode e.Index
          >
          t.Context s.OldMode s.Depth;

        t.Context^ # CAlreadyBounded s.Depth =
          <EL-AddErrorAt
            t.ErrorList t.SrcPos
            'Variable hide operator^ must be applied to first occurence of '
            'variable ' s.Mode '.' e.Index
          >
          t.Context s.Depth s.Mode;
      }
    >;
}

StrFromBracket {
  TkOpenBracket = '('; TkCloseBracket = ')';
  TkOpenCall = '<'; TkCloseCall = '>';
  TkOpenADT = '['; TkCloseADT = ']';
  #Brackets = '(';
  #ADT-Brackets = '[';
  #CallBrackets = '<';
}

//FROM Context
$EXTERN Cntx-CheckVariable;

/*
  <ParseResult
    (e.Functions) t.ErrorList t.SymTable t.Context
    (e.MultiBracket) (e.Scanned) e.Tail
  >
*/
ParseResult {
  // Обработка литералов атомов

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (TkChar t.SrcPos s.Char) e.Tail =
    <ParseResult
      (e.Functions) t.ErrorList t.SymTable t.Context
      (e.MultiBracket) (e.Scanned (TkChar s.Char)) e.Tail
    >;

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (TkNumber t.SrcPos s.Number) e.Tail =
    <ParseResult
      (e.Functions) t.ErrorList t.SymTable t.Context
      (e.MultiBracket) (e.Scanned (TkNumber s.Number)) e.Tail
    >;

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (TkName t.SrcPos e.Name) e.Tail =
    <ParseResult
      (e.Functions)
      t.ErrorList <ST-AddFunctionCall t.SymTable t.SrcPos e.Name>
      t.Context
      (e.MultiBracket) (e.Scanned (TkName e.Name)) e.Tail
    >;

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket)
  (e.Scanned)
  (TkIdentMarker t.MarkerSrcPos) (TkName t.NameSrcPos e.Name) e.Tail =
    <ParseResult
      (e.Functions)
      t.ErrorList <ST-AddIdent t.SymTable e.Name>
      t.Context
      (e.MultiBracket) (e.Scanned (TkIdentifier e.Name)) e.Tail
    >;

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (TkIdentMarker t.SrcPos) e.Tail =
    <ParseResult
      (e.Functions)
      <EL-AddErrorAt t.ErrorList t.SrcPos 'Missed name after "#"'>
      t.SymTable t.Context
      (e.MultiBracket) (e.Scanned) e.Tail
    >;

  // Переменные

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (TkVariable t.SrcPos s.Mode e.Index) e.Tail =
    <Fetch
      <CheckUseVariable t.ErrorList t.Context t.SrcPos s.Mode e.Index>
      {
        t.ErrorList^ t.Context^ s.Mode^ s.Depth =
          <ParseResult
            (e.Functions)
            t.ErrorList t.SymTable t.Context
            (e.MultiBracket)
            (e.Scanned (TkVariable s.Mode e.Index s.Depth)) e.Tail
          >;

        t.ErrorList^ t.Context^ =
          <ParseResult
            (e.Functions)
            t.ErrorList t.SymTable t.Context
            (e.MultiBracket) (e.Scanned) e.Tail
          >;
      }
    >;

  /*
    Скобки.

    Стратегия обработки ошибок: если мы встретили "не ту"
    закрывающую скобку, считаем, что "та" была пропущена.
    Если имеем непарную открывающую скобку, считаем,
    что соответствующая скобка была пропущена в конце выражения;
    если имеем непарную закрывающую скобку, считаем её лишней.
  */

  // Хитро обрабатываются АДТ-скобки,
  // т.к. требуется, чтобы после них было имя.

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket)
  (e.Scanned)
  (# COpen t.SrcPosADT TkOpenADT) (TkName t.SrcPosName e.Name) e.Tail =
    <ParseResult
      (e.Functions)
      t.ErrorList <ST-AddFunctionCall t.SymTable t.SrcPosName e.Name>
      t.Context
      (
        e.MultiBracket
        (#ADT-Brackets t.SrcPosADT TkCloseADT e.Scanned)
      )
      ((e.Name)) e.Tail
    >;

  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (# COpen t.SrcPos TkOpenADT) e.Tail =
    <ParseResult
      (e.Functions)
      <EL-AddErrorAt
        t.ErrorList t.SrcPos 'Expected name after "["'
      >
      t.SymTable t.Context
      (
        e.MultiBracket
        (#ADT-Brackets t.SrcPos TkCloseADT e.Scanned)
      )
      ((<ST-GetAnyName t.SymTable>)) e.Tail
    >;

  // Структурные и угловые скобки обрабатываются одинаково
  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (# COpen t.SrcPos s.OpenBracket) e.Tail =
    <ParseResult
      (e.Functions) t.ErrorList t.SymTable t.Context
      (
        e.MultiBracket
        (
          <Fetch
            s.OpenBracket
            {
              TkOpenBracket = #Brackets;
              TkOpenCall = #CallBrackets;
            }
          >
          t.SrcPos
          <Fetch
            s.OpenBracket
            {
              TkOpenBracket = TkCloseBracket;
              TkOpenCall = TkCloseCall;
            }
          >
          e.Scanned
        )
      )
      () e.Tail
    >;

  // Правильная закрывающая скобка
  (e.Functions) t.ErrorList t.SymTable t.Context
  (
    e.MultiBracket
    (s.BracketsSign t.OpenSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (# CClose t.SrcPos s.CloseBracket) e.Tail =
    <ParseResult
      (e.Functions) t.ErrorList t.SymTable t.Context
      (e.MultiBracket) (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  // Лямбда-функция
  (e.Functions) t.ErrorList t.SymTable t.Context
  (e.MultiBracket) (e.Scanned) (TkOpenBlock t.SrcPos) e.Tail =
    <ParseResult-Block
      (e.MultiBracket) (e.Scanned)
      <ParseBlock
        (e.Functions) t.ErrorList t.SymTable t.Context
        e.Tail
      >
    >;

  // Неправильная закрывающая скобка или закрывающая скобка отсутствует
  (e.Functions) t.ErrorList t.SymTable t.Context
  (
    e.MultiBracket
    (s.BracketsSign t.OpenSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) e.Tail =
    <ParseResult
      (e.Functions)
      <EL-AddErrorAt
        t.ErrorList t.OpenSrcPos
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      t.SymTable t.Context
      (e.MultiBracket) (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  (e.Functions) t.ErrorList t.SymTable t.Context
  () (e.Scanned) (# CClose t.SrcPos s.CloseBracket) e.Tail =
    <ParseResult
      (e.Functions)
      <EL-AddErrorAt
        t.ErrorList t.SrcPos
        'Unbalanced bracket "' <StrFromBracket s.CloseBracket> '"'
      >
      t.SymTable t.Context
      () (e.Scanned) e.Tail
    >;

  (e.Functions) t.ErrorList t.SymTable t.Context
  () (e.Result) e.Tail =
    (e.Functions) t.ErrorList t.SymTable t.Context
    (e.Result) e.Tail;
}

$LABEL CExistVariable, CNotFound;

CheckUseVariable {
  t.ErrorList t.Context t.SrcPos s.Mode e.Index =
    <Fetch
      <Cntx-CheckVariable t.Context s.Mode e.Index>
      {
        t.Context^ # CExistVariable s.Depth =
          t.ErrorList t.Context s.Mode s.Depth;

        t.Context^ # CInvalidMode s.Depth s.OldMode =
          <InvalidModeVariableError
            t.ErrorList t.SrcPos s.OldMode s.Mode e.Index
          >
          t.Context s.OldMode s.Depth;

        t.Context^ # CNotFound =
          <EL-AddErrorAt
            t.ErrorList t.SrcPos 'Variable ' s.Mode '.' e.Index ' not found'
          >
          t.Context;
      }
    >;
}

$LABEL CCreateClosure;

ParseResult-Block {
  (e.MultiBracket) (e.Scanned)
  (s.NextNum e.Functions) t.ErrorList t.SymTable
  ( t.Context /* свободные переменные в функции отсутствуют */ )
  e.Tail =
    <ParseResult
      (s.NextNum e.Functions) t.ErrorList t.SymTable t.Context
      (e.MultiBracket)
      (e.Scanned (TkName # CLambdaName <Dec s.NextNum>))
      e.Tail
    >;

  (e.MultiBracket) (e.Scanned)
  (s.NextNum e.Functions) t.ErrorList t.SymTable
  ( t.Context e.FreeContext )
  e.Tail =
    <Fetch
      <Dec s.NextNum> (<CreateContext e.FreeContext>)
      {
        s.FunctionNumber (e.RealContext) =
          <ParseResult
            (s.NextNum e.Functions) t.ErrorList t.SymTable t.Context
            (e.MultiBracket)
            (
              e.Scanned
              (#CallBrackets
                (TkName # CCreateClosure)
                (TkName # CLambdaName s.FunctionNumber)
                e.RealContext
              )
            )
            e.Tail
          >;
      }
    >;
}
