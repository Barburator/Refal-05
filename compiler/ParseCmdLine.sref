$EENUM CmdLineError, CompileList, CmdNone;

/**
  <ParseCommandLine e.Arguments>
    == t.CommandLineInfo CompileList (e.Folders) t.FoundedFile*
    == (CmdLineError e.Message)

  t.FoundedFile ::= (e.FileName)
*/
$ENTRY ParseCommandLine {
  e.Arguments =
    <DoParseCommandLine
      <CollectorObject (/* folders */) /* files */> e.Arguments
    >;
}

/**
  <CmdCompileCommand t.CommandLineInfo>
    == CmdNone
    == e.Command
*/
$ENTRY CmdCompileCommand {
  s.CommandLineInfo = <s.CommandLineInfo CmdCompileCommand>;
}

$ENUM CAddCompileCommand, CAddErrorFile, CGetInfo, CAddFile, CAddFolder,
  CSetOutput;

DoParseCommandLine {
  s.Collector ('-c') (e.CompileCommand) e.Arguments =
    <DoParseCommandLine
      <s.Collector CAddCompileCommand e.CompileCommand> e.Arguments
    >;

  s.Collector ('-c') =
    (CmdLineError 'After option ''-c'' expected C++ compiler command line');

  s.Collector ('-d') (e.Directory) e.Arguments =
    <DoParseCommandLine
      <s.Collector CAddFolder e.Directory> e.Arguments
    >;

  s.Collector ('-d') =
    (CmdLineError 'After option ''-d'' expected find directory');

  s.Collector ('-o') (e.OutputFile) e.Arguments =
    <DoParseCommandLine
      <s.Collector CSetOutput e.OutputFile> e.Arguments
    >;

  s.Collector ('--') e.Arguments =
    <DoParseFileNamesOnly
      s.Collector e.Arguments
    >;

  s.Collector (e.NextFileName) e.Arguments =
    <DoParseCommandLine
      <s.Collector CAddFile e.NextFileName> e.Arguments
    >;

  s.Collector = <s.Collector CGetInfo>;
}

DoParseFileNamesOnly {
  s.Collector (e.NextFileName) e.Files =
    <DoParseFileNamesOnly
      <s.Collector CAddFile e.NextFileName> e.Files
    >;

  s.Collector = <s.Collector CGetInfo>;
}

//------------------------------------------------------------------------------

CollectorObject {
  (e.Folders) e.Files =
    {
      CAddCompileCommand e.Command =
        <Collector-Compiler (e.Folders) (e.Files) e.Command>;

      CAddFile e.FileName =
        <CollectorObject (e.Folders) e.Files (e.FileName)>;

      CAddFolder e.FolderName =
        <CollectorObject (e.Folders (e.FolderName)) e.Files>;

      CGetInfo =
        {
          CmdCompileCommand = CmdNone;
        }
        CompileList (e.Folders) e.Files;
    };
}

Collector-Compiler {
  (e.Folders) (e.Files) e.Command =
    {
      CAddCompileCommand e.NewCommand = <Collector-Error-CC>;

      CAddFile e.FileName =
        <Collector-Compiler
          (e.Folders) (e.Files (e.FileName)) e.Command
        >;

      CAddFolder e.FolderName =
        <Collector-Compiler
          (e.Folders (e.FolderName)) (e.Files) e.Command
        >;

      CGetInfo =
        {
          CmdCompileCommand = e.Command;
        }
        CompileList (e.Folders) e.Files;
    };
}

Collector-Error-CC {
  = <Collector-Error 'Multiple declaration of C++ compiler command line'>;
}

Collector-Error {
  e.Message =
    {
      CAddCompileCommand e.Command = <Collector-Error e.Message>;

      CAddFile e.FileName = <Collector-Error e.Message>;

      CAddFolder e.FolderName = <Collector-Error e.Message>;

      CGetInfo (e.Folders) e.Files = (CmdLineError e.Message);
    };
}
