*$FROM R05-Lexer
$EXTERN R05-LexScan-File, R05-LexScan-String, TkEOF, TkExtern, TkEnum, TkEEnum,
  TkEntry, TkName, TkSemicolon, TkNative, TkComma, TkOpenBlock, TkCloseBlock,
  TkChar, TkNumber, TkVariable, TkOpen, Bracket, CallBracket, TkClose,
  TkReplace, TkError, TkUnexpected, StrFromToken;

*$FROM LibraryEx
$EXTERN Success, Fails, Map, Reduce;

*$FROM R05-AST
$EXTERN Extern, Local, Entry, Native, Function, Sentences, Symbol, Char, Number,
  Name, Variable, Brackets, CallBrackets;


/**
  <R05-Parse-File e.SourceFile>
    == Success e.Tree
    == Fails e.Errors
*/
$ENTRY R05-Parse-File {
  e.SourceFile = <R05-Parse-Tokens <R05-LexScan-File e.SourceFile>>;
}

/**
  <R05-Parse-String e.String>
    == Success e.Tree
    == Fails e.Errors
*/
$ENTRY R05-Parse-String {
  e.String = <R05-Parse-Tokens <R05-LexScan-String e.String>>;
}

/**
  <R05-Parse-Tokens e.Tokens>
    == Success e.Tree
    == Fails e.Errors
*/
$ENTRY R05-Parse-Tokens {
  e.Tokens =
    <R05-Parse-Aux
      <SemanticCheck
        <ParseElements
          <EL-Create>
          (/* имена вызываемых функций */)
          (/* сгенерированное дерево */)
          e.Tokens
        >
      >
    >
}

R05-Parse-Aux {
  t.ErrorList e.Tree =
    <R05-Parse-SwErrors (<EL-Destroy t.ErrorList>) e.Tree>;
}

R05-Parse-SwErrors {
  (/* no errors */) e.Tree = Success e.Tree;

  (e.Errors) e.Tree = Fails <SortErrors e.Errors>;
}

SortErrors {
  e.Errors = <DoSortErrors (/* sorted */) e.Errors>;
}

DoSortErrors {
  (e.Sorted) /* unsorted */ = e.Sorted;

  (e.Sorted) t.Next e.Unsorted =
    <DoSortErrors
      (<SortErrors-Insert e.Sorted t.Next>)
      e.Unsorted
    >;
}

SortErrors-Insert {
  /* пусто */ t.Next = t.Next;

  e.Sorted t.Great t.Next =
    <SortErrors-Insert-Compare
      e.Sorted t.Great t.Next <CompareError t.Great t.Next>
    >;
}

SortErrors-Insert-Compare {
  e.Sorted t.Great t.Next '+' = <SortErrors-Insert e.Sorted t.Next> t.Great;
  e.Sorted t.Great t.Next s.Other = e.Sorted t.Great t.Next;
}

CompareError {
  ((s.Row s.Col1) e.Message1) ((s.Row s.Col2) e.Message2) =
    <Compare s.Col1 s.Col2>;

  ((s.Row1 s.Col1) e.Message1) ((s.Row2 s.Col2) e.Message2) =
    <Compare s.Row1 s.Row2>;
}


/*
  Трансляция осуществляется обычным методом рекурсивного спуска.

  TranslationUnit = Element* .

  Element =
    '$ENUM' NameList |
    '$EENUM' NameList |
    '$EXTERN' NameList |
    '$ENTRY' Function |
    Function .

  NameList = Name ',' NameList | Name ';' .

  Function = Name Block .

  Block = '{' Sentence* '}' .

  Sentence = Pattern '=' Result ';' .

  Pattern = PatternTerm* .

  PatternTerm = CommonTerm | '(' Pattern ')' | .

  CommonTerm = Char | Number | Name | Variable .

  Result = ResultTerm* .

  ResultTerm = CommonTerm | '(' Result ')' | '<' Result '>' .

  Стратегия обработки ошибок. Выделяем только одну функцию, проглатывающую
  неожиданные лексемы. Остальные функции при обнаружении неожиданной лексемы
  должны её оставить в лексической свёртке перейдя в одно из состояний уровнем
  выше. Данная стратегия обработки ошибок может нарушаться, но при этом должно
  выполняться условие, что TkEOF передаётся в ParseElements.
*/

/*
  TranslationUnit = Element* .
*/

/*
  Element =
    '$ENUM' NameList |
    '$EENUM' NameList |
    '$EXTERN' NameList |
    '$ENTRY' Function |
    Function .
*/

ParseElements {
  t.ErrorList (e.References) (e.Generated) (TkEOF t.SrcPos) =
    t.ErrorList (e.References) e.Generated;

  t.ErrorList (e.References) (e.Generated)
  (TkExtern t.SrcPos) e.Tail =
    <ParseElements-ElementParsed
      (e.Generated)
      <ParseNameList (/* generated */) t.ErrorList (e.References) Extern e.Tail>
    >;

  t.ErrorList (e.References) (e.Generated)
  (TkEnum t.SrcPos) e.Tail =
    <ParseElements-ElementParsed
      (e.Generated)
      <ParseNameList (/* generated */) t.ErrorList (e.References) Local e.Tail>
    >;

  t.ErrorList (e.References) (e.Generated)
  (TkEEnum t.SrcPos) e.Tail =
    <ParseElements-ElementParsed
      (e.Generated)
      <ParseNameList (/* generated */) t.ErrorList (e.References) Entry e.Tail>
    >;

  t.ErrorList (e.References) (e.Generated)
  (TkEntry t.EntrySrcPos) (TkName t.NameSrcPos e.Name) e.Tail =
    <ParseElements-ElementParsed
      (e.Generated)
      <ParseFunction
        Entry (e.Name) t.NameSrcPos t.ErrorList (e.References) e.Tail
      >
    >;

  t.ErrorList (e.References) (e.Generated)
  (TkEntry t.EntrySrcPos) t.Unexpected e.Tail =
    <ParseElements
      <EL-AddUnexpected t.ErrorList t.Unexpected 'function name'>
      (e.References) (e.Generated) t.Unexpected e.Tail
    >;

  t.ErrorList (e.References) (e.Generated) (TkName t.SrcPos e.Name) e.Tail =
    <ParseElements-ElementParsed
      (e.Generated)
      <ParseFunction Local (e.Name) t.SrcPos t.ErrorList (e.References) e.Tail>
    >;

  t.ErrorList (e.References) (e.Generated) (TkSemicolon t.SrcPos) e.Tail =
    <ParseElements t.ErrorList (e.References) (e.Generated) e.Tail>;

  t.ErrorList (e.References) (e.Generated) (TkNative t.SrcPos e.Native) e.Tail =
    <ParseElements
      t.ErrorList (e.References) (e.Generated (Native e.Native)) e.Tail
    >;

  t.ErrorList (e.References) (e.Generated) t.Unexpected e.Tail =
    <ParseElements
      <EL-AddUnexpected t.ErrorList t.Unexpected 'directive or name'>
      (e.References) (e.Generated) e.Tail
    >;
}

ParseElements-ElementParsed {
  (e.Generated) (e.NewGenerated) t.ErrorList (e.References) e.Tail =
    <ParseElements
      t.ErrorList (e.References) (e.Generated e.NewGenerated) e.Tail
    >;
}

/*
  NameList = Name ',' NameList | Name ';' .
*/

ParseNameList {
  (e.Generated) t.ErrorList (e.References)
  s.ScopeClass (TkName t.SrcPos e.Name) e.Tail =
    <ParseNameList-Tail
      (
        e.Generated
        (Function t.SrcPos s.ScopeClass (e.Name) Sentences /* пусто */)
      )
      t.ErrorList (e.References) s.ScopeClass e.Tail
    >;

  (e.Generated) t.ErrorList (e.References) s.ScopeClass t.Unexpected e.Tail =
    (e.Generated)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'function name'>
    (e.References) t.Unexpected e.Tail;
}

ParseNameList-Tail {
  (e.Generated) t.ErrorList (e.References)
  s.ScopeClass (TkComma t.SrcPos) e.Tail =
    <ParseNameList
      (e.Generated) t.ErrorList (e.References) s.ScopeClass e.Tail
    >;

  (e.Generated) t.ErrorList (e.References)
  s.ScopeClass (TkSemicolon t.SrcPos) e.Tail =
    (e.Generated) t.ErrorList (e.References) e.Tail;

  (e.Generated) t.ErrorList (e.References) s.ScopeClass t.Unexpected e.Tail =
    (e.Generated)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'comma or semicolon'>
    (e.References) t.Unexpected e.Tail;
}

/*
  Function = Name '{' Sentence* '}'
*/

ParseFunction {
  s.ScopeClass (e.Name) t.NameSrcPos t.ErrorList (e.References)
  (TkOpenBlock t.SrcPos) e.Tail =
    <ParseFunction-BlockParsed
      s.ScopeClass (e.Name) t.NameSrcPos
      <ParseBlock t.ErrorList (e.References) e.Tail>
    >;

  s.ScopeClass (e.Name) t.NameSrcPos t.ErrorList (e.References)
  t.Unexpected e.Tail =
    (/* ничего не сгенерировали */)
    <EL-AddUnexpected t.ErrorList t.Unexpected 'open brace'>
    (e.References) t.Unexpected e.Tail;
}

ParseFunction-BlockParsed {
  s.ScopeClass (e.Name) t.SrcPos (e.Body) t.ErrorList (e.References) e.Tail =
    ((Function t.SrcPos s.ScopeClass (e.Name) e.Body))
    t.ErrorList (e.References) e.Tail;
}

/*
  <ParseBlock t.ErrorList (e.References) e.Tokens>
    == t.ErrorList (e.References) e.Tokens

  t.FunctionBody ::= (((e.Pattern) (e.Result))*)

  Открывающая скобка должна отсутствовать во входном потоке.
*/
ParseBlock {
  t.ErrorList (e.References) (TkNative t.SrcPos e.Native) e.Tail =
    (Native e.Native)
    <ParseBlock-CloseBlock t.ErrorList (e.References) e.Tail>;

  t.ErrorList (e.References) e.Tokens =
    <DoParseBlock
      t.ErrorList (e.References) (/* sentences */) e.Tokens
    >;
}

ParseBlock-CloseBlock {
  t.ErrorList (e.References) (TkCloseBlock t.SrcPos) e.Tail =
    t.ErrorList (e.References) e.Tail;

  t.ErrorList (e.References) t.Unexpected e.Tail =
    <EL-AddUnexpected t.ErrorList t.Unexpected '"}"'>
    (e.References) t.Unexpected e.Tail;
}

DoParseBlock {
  t.ErrorList (e.References) (e.Sentences) (TkCloseBlock t.SrcPos) e.Tail =
    (Sentences e.Sentences) t.ErrorList (e.References) e.Tail;

  t.ErrorList (e.References) (e.Sentences) (TkEOF t.SrcPos) e.Tail =
    (Sentences e.Sentences)
    <EL-AddErrorAt
      t.ErrorList t.SrcPos 'Unexpected EOF, expected "}"'
    >
    (e.References)
    (TkEOF t.SrcPos) e.Tail;

  t.ErrorList (e.References) (e.Sentences) e.Tokens =
    <ParseSentence t.ErrorList (e.References) (e.Sentences) e.Tokens>;
}

ParseSentence {
  t.ErrorList (e.References) (e.Sentences) e.Tokens =
    <ParseSentence-Aux
      (e.Sentences)
      <ParsePattern t.ErrorList (e.References) e.Tokens>
    >;
}

ParseSentence-Aux {
  (e.Sentences) (e.Pattern) (e.Result)
  t.ErrorList (e.References) (TkSemicolon t.SrcPos) e.Tokens =
    <DoParseBlock
      t.ErrorList (e.References) (e.Sentences ((e.Pattern) (e.Result))) e.Tokens
    >;

  (e.Sentences) (e.Pattern) (e.Result)
  t.ErrorList (e.References) (TkCloseBlock t.SrcPos) e.Tokens =
    (Sentences e.Sentences ((e.Pattern) (e.Result)))
    t.ErrorList (e.References) e.Tokens;

  (e.Sentences) (e.Pattern) (e.Result)
  t.ErrorList (e.References) t.Unexpected e.Tokens =
    (Sentences e.Sentences ((e.Pattern) (e.Result)))
    <EL-AddUnexpected t.ErrorList t.Unexpected '"=" or ";"'>
    (e.References) t.Unexpected e.Tokens;
}

ParsePattern {
  t.ErrorList (e.References) e.Tokens =
    <DoParsePattern
      t.ErrorList (e.References) (/* таблица переменных */) (/* мультискобки */)
      (/* просканировано */) e.Tokens
    >;
}

DoParsePattern {
  /* Обработка литералов атомов */

  t.ErrorList (e.References) (e.Variables) (e.MultiBrackets)
  (e.Scanned) (TkChar t.SrcPos s.Char) e.Tail =
    <DoParsePattern
      t.ErrorList (e.References) (e.Variables) (e.MultiBrackets)
      (e.Scanned (Symbol Char s.Char)) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables) (e.MultiBrackets)
  (e.Scanned) (TkNumber t.SrcPos s.Number) e.Tail =
    <DoParsePattern
      t.ErrorList (e.References) (e.Variables) (e.MultiBrackets)
      (e.Scanned (Symbol Number s.Number)) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables) (e.MultiBrackets)
  (e.Scanned) (TkName t.SrcPos e.Name) e.Tail =
    <DoParsePattern
      t.ErrorList (e.References (t.SrcPos e.Name)) (e.Variables)
      (e.MultiBrackets)
      (e.Scanned (Symbol Name e.Name)) e.Tail
    >;

  /* Переменные */

  t.ErrorList (e.References)
  (e.Variables-B (s.Mode e.Index) e.Variables-E)
  (e.MultiBracket)
  (e.Scanned) (TkVariable t.SrcPos s.Mode e.Index) e.Tail =
    <DoParsePattern
      t.ErrorList (e.References)
      (e.Variables-B (s.Mode e.Index) e.Variables-E)
      (e.MultiBracket)
      (e.Scanned (Variable s.Mode e.Index)) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables) (e.MultiBracket)
  (e.Scanned) (TkVariable t.SrcPos s.Mode e.Index) e.Tail =
    <DoParsePattern
      t.ErrorList (e.References) ((s.Mode e.Index) e.Variables) (e.MultiBracket)
      (e.Scanned (Variable s.Mode e.Index)) e.Tail
    >;

  /*
    Скобки.

    Стратегия обработки ошибок: если мы встретили "не ту" закрывающую
    скобку, считаем, что "та" была пропущена.
    Если имеем непарную открывающую скобку, считаем,
    что соответствующая скобка была пропущена в конце выражения;
    если имеем непарную закрывающую скобку, считаем её лишней.
  */

  t.ErrorList (e.References) (e.Variables) (e.MultiBracket)
  (e.Scanned) (TkOpen t.SrcPos Bracket) e.Tail =
    <DoParsePattern
      t.ErrorList (e.References) (e.Variables)
      (
        e.MultiBracket
        (Brackets t.SrcPos Bracket e.Scanned)
      )
      () e.Tail
    >;

  t.ErrorList (e.References) (e.Variables) (e.MultiBracket)
  (e.Scanned) (TkOpen t.SrcPos CallBracket) e.Tail =
    <DoParsePattern
      <EL-AddErrorAt
        t.ErrorList t.SrcPos 'Unexpected call bracket in pattern expression'
      >
      (e.References) (e.Variables)
      (
        e.MultiBracket
        (Brackets t.SrcPos CallBracket e.Scanned)
      )
      () e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  (
    e.MultiBracket
    (s.BracketsSign t.InnerSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (TkClose t.CloseSrcPos s.CloseBracket) e.Tail =
    <DoParsePattern
      t.ErrorList (e.References) (e.Variables) (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  (
    e.MultiBracket
    (s.BracketsSign t.InnerSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (TkClose t.SrcPos s.OtherCloseBracket) e.Tail =
    <DoParsePattern
      <EL-AddErrorAt
        t.ErrorList t.InnerSrcPos
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      (e.References) (e.Variables) (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      (TkClose t.SrcPos s.OtherCloseBracket) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables) ()
  (e.Scanned) (TkClose t.SrcPos s.CloseBracket) e.Tail =
    <DoParsePattern
      <EL-AddErrorAt
        t.ErrorList t.SrcPos
        'Unbalanced bracket "' <StrFromBracket s.CloseBracket> '"'
      >
      (e.References) (e.Variables) ()
      (e.Scanned) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  (
    e.MultiBracket
    (s.BracketsSign t.InnerSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) e.Tail =
    <DoParsePattern
      <EL-AddErrorAt
        t.ErrorList t.InnerSrcPos
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      (e.References) (e.Variables) (e.MultiBracket)
      (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList (e.References) (e.Variables) ()
  (e.Pattern) e.Tail =
    /* Выбрасываем образец из рекурсивого вызова */
    (e.Pattern)
    <ParsePattern-ExpectReplace
      t.ErrorList (e.References) (e.Variables) e.Tail
    >;
}

StrFromBracket {
  Bracket = ')';
  CallBracket = '>';
  Brackets = '(';
  CallBrackets = '<';
}

ParsePattern-ExpectReplace {
  t.ErrorList (e.References) (e.Variables) (TkReplace t.SrcPos) e.Tokens =
    <ParseResult
      t.ErrorList (e.References) (e.Variables) e.Tokens
    >;

  t.ErrorList (e.References) (e.Variables) t.Unexpected e.Tokens =
    <ParseResult
      <EL-AddUnexpected t.ErrorList t.Unexpected '"="'>
      (e.References) (e.Variables) t.Unexpected e.Tokens
    >;
}

ParseResult {
  t.ErrorList (e.References) (e.Variables) e.Tokens =
    <DoParseResult
      t.ErrorList (e.References) (e.Variables)
      (/* мультискобки */) (/* просканировано */) e.Tokens
    >;
}

/*
  <DoParseResult
    t.ErrorList (e.References) (e.Variables)
    (e.MultiBracket) (e.Scanned) e.Tail
  >
*/
DoParseResult {
  /* Обработка литералов атомов */

  t.ErrorList (e.References) (e.Variables)
  (e.MultiBracket) (e.Scanned) (TkChar t.SrcPos s.Char) e.Tail =
    <DoParseResult
      t.ErrorList (e.References) (e.Variables)
      (e.MultiBracket) (e.Scanned (Symbol Char s.Char)) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  (e.MultiBracket) (e.Scanned) (TkNumber t.SrcPos s.Number) e.Tail =
    <DoParseResult
      t.ErrorList (e.References) (e.Variables)
      (e.MultiBracket) (e.Scanned (Symbol Number s.Number)) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  (e.MultiBracket) (e.Scanned) (TkName t.SrcPos e.Name) e.Tail =
    <DoParseResult
      t.ErrorList (e.References (t.SrcPos e.Name)) (e.Variables)
      (e.MultiBracket) (e.Scanned (Symbol Name e.Name)) e.Tail
    >;

  /* Переменные */

  t.ErrorList (e.References) (e.Variables-B (s.Mode e.Index) e.Variables-E)
  (e.MultiBracket) (e.Scanned) (TkVariable t.SrcPos s.Mode e.Index) e.Tail =
    <DoParseResult
      t.ErrorList (e.References) (e.Variables-B (s.Mode e.Index) e.Variables-E)
      (e.MultiBracket) (e.Scanned (Variable s.Mode e.Index)) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  (e.MultiBracket) (e.Scanned) (TkVariable t.SrcPos s.Mode e.Index) e.Tail =
    <DoParseResult
      <EL-AddErrorAt
        t.ErrorList t.SrcPos 'Variable ' s.Mode '.' e.Index ' not found'
      >
      (e.References) (e.Variables) (e.MultiBracket) (e.Scanned) e.Tail
    >;

  /*
    Скобки.

    Стратегия обработки ошибок: если мы встретили "не ту"
    закрывающую скобку, считаем, что "та" была пропущена.
    Если имеем непарную открывающую скобку, считаем,
    что соответствующая скобка была пропущена в конце выражения;
    если имеем непарную закрывающую скобку, считаем её лишней.
  */

  /* Структурные и угловые скобки обрабатываются одинаково */
  t.ErrorList (e.References) (e.Variables)
  (e.MultiBracket) (e.Scanned) (TkOpen t.SrcPos s.OpenBracket) e.Tail =
    <DoParseResult
      t.ErrorList (e.References) (e.Variables)
      (
        e.MultiBracket
        (
          <BracketPairName s.OpenBracket>
          t.SrcPos
          s.OpenBracket
          e.Scanned
        )
      )
      () e.Tail
    >;

  /* Правильная закрывающая скобка */
  t.ErrorList (e.References) (e.Variables)
  (
    e.MultiBracket
    (s.BracketsSign t.OpenSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) (TkClose t.SrcPos s.CloseBracket) e.Tail =
    <DoParseResult
      t.ErrorList (e.References) (e.Variables)
      (e.MultiBracket) (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  /* Неправильная закрывающая скобка или закрывающая скобка отсутствует */
  t.ErrorList (e.References) (e.Variables)
  (
    e.MultiBracket
    (s.BracketsSign t.OpenSrcPos s.CloseBracket e.Scanned)
  )
  (e.InnerScanned) e.Tail =
    <DoParseResult
      <EL-AddErrorAt
        t.ErrorList t.OpenSrcPos
        'Unbalanced bracket "' <StrFromBracket s.BracketsSign> '"'
      >
      (e.References) (e.Variables)
      (e.MultiBracket) (e.Scanned (s.BracketsSign e.InnerScanned))
      e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  () (e.Scanned) (TkClose t.SrcPos s.CloseBracket) e.Tail =
    <DoParseResult
      <EL-AddErrorAt
        t.ErrorList t.SrcPos
        'Unbalanced bracket "' <StrFromBracket s.CloseBracket> '"'
      >
      (e.References) (e.Variables)
      () (e.Scanned) e.Tail
    >;

  t.ErrorList (e.References) (e.Variables)
  () (e.Result) e.Tail =
    (e.Result) t.ErrorList (e.References) e.Tail;
}

BracketPairName {
  Bracket = Brackets;
  CallBracket = CallBrackets;
}


SemanticCheck {
  t.ErrorList (e.References) e.Tree =
    <CheckRepeatedDefinitions
      t.ErrorList (e.References) <LoadBuiltins> e.Tree
    >;
}

LoadBuiltins {
  = <Map parser_BuiltinDeclaration <ListOfBuiltin>>;
}

$ENTRY parser_BuiltinDeclaration {
  (s.Number s.Name s.Type) = <Normalize <Explode s.Name>>;
}

Normalize {
  /* пусто */ = /* пропускаем */;
  e.Name '$' = /* пропускаем */;

  e.Name-B '-' e.Name-E = <Normalize e.Name-B '_' e.Name-E>;

  e.Name = (Function NO-POS Extern (e.Name) Sentences /* пусто */);
}

*$ENUM NO-POS

CheckRepeatedDefinitions {
  t.ErrorList (e.References)
  e.Tree-B (Function t.SrcPos1 s.ScopeClass (e.Name) e.Body) e.Tree-E
  (Function t.SrcPos2 Extern (e.Name) Sentences) =
    /* $EXTERN для уже существующей функции */
    <CheckRepeatedDefinitions
      t.ErrorList (e.References)
      e.Tree-B (Function t.SrcPos1 s.ScopeClass (e.Name) e.Body) e.Tree-E
    >;

  t.ErrorList (e.References-B (t.SrcPosRef e.Name) e.References-E)
  e.Tree (Function t.SrcPos Extern (e.Name) Sentences) =
    /* $EXTERN используется */
    <CheckRepeatedDefinitions
      t.ErrorList (e.References-B <RemoveReference (e.Name) e.References-E>)
      e.Tree
    >
    (Extern e.Name);

  t.ErrorList (e.References)
  e.Tree (Function t.SrcPos Extern (e.Name) Sentences) =
    /* $EXTERN не используется */
    <CheckRepeatedDefinitions t.ErrorList (e.References) e.Tree>;

  t.ErrorList (e.References)
  e.Tree-B (Function t.SrcPos1 Extern (e.Name) Sentences) e.Tree-E
  (Function t.SrcPos2 s.ScopeClass (e.Name) e.Body) =
    /* $EXTERN для уже существующей функции */
    <CheckRepeatedDefinitions
      t.ErrorList (e.References)
      e.Tree-B e.Tree-E (Function t.SrcPos2 s.ScopeClass (e.Name) e.Body)
    >;

  t.ErrorList (e.References)
  e.Tree-B (Function t.SrcPos1 s.ScopeClass1 (e.Name) e.Body1) e.Tree-E
  (Function t.SrcPos2 s.ScopeClass2 (e.Name) e.Body2) =
    /* Повторное определение функции */
    <CheckRepeatedDefinitions
      <EL-AddErrorAt
        t.ErrorList t.SrcPos2 'Function ' e.Name ' already defined'
      >
      (e.References)
      e.Tree-B (Function t.SrcPos1 s.ScopeClass1 (e.Name) e.Body1) e.Tree-E
    >;

  t.ErrorList (e.References)
  e.Tree (Function t.SrcPos s.ScopeClass (e.Name) e.Body) =
    <CheckRepeatedDefinitions
      t.ErrorList (<RemoveReference (e.Name) e.References>) e.Tree
    >
    (Function s.ScopeClass (e.Name) e.Body);

  t.ErrorList (e.References) e.Tree (Native e.Native) =
    <CheckRepeatedDefinitions t.ErrorList (e.References) e.Tree>
    (Native e.Native);

  t.ErrorList (e.References) /* пусто */ =
    <Reduce parser_AddUnresolved t.ErrorList e.References>;
}

RemoveReference {
  (e.Name) e.References-B (t.SrcPos e.Name) e.References-E =
    e.References-B <RemoveReference (e.Name) e.References-E>;

  (e.Name) e.References = e.References;
}

$ENTRY parser_AddUnresolved {
  t.ErrorList (t.SrcPos e.Name) =
    <EL-AddErrorAt t.ErrorList t.SrcPos 'Function ' e.Name ' is not defined'>;
}


*$ENUM ErrorList

EL-Create {
  = (ErrorList);
}

EL-AddErrorAt {
  (ErrorList e.Errors) t.SrcPos e.Message =
    (ErrorList e.Errors (t.SrcPos e.Message));
}

EL-AddUnexpected {
  t.ErrorList (TkError t.SrcPos e.Message) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos e.Message
    >;

  t.ErrorList (TkUnexpected t.SrcPos e.Unexpected) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos
      'Unknown characters "' e.Unexpected '"'
    >;

  t.ErrorList (s.Unexpected t.SrcPos e.Info) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos
      'Unexpected ' <StrFromToken s.Unexpected e.Info>
      ', expected ' e.Expected
    >;
}

EL-Destroy {
  (ErrorList e.Errors) = e.Errors;
}
