*$FROM Lexer
$EXTERN TkError, TkUnexpected, StrFromToken;

*$FROM LibraryEx
$EXTERN Map;


*$ENUM ErrorList

$ENTRY EL-Create {
  = (ErrorList);
}

$ENTRY EL-AddErrorAt {
  (ErrorList e.Errors) t.SrcPos e.Message =
    (ErrorList e.Errors (t.SrcPos e.Message));
}

$ENTRY EL-AddUnexpected {
  t.ErrorList (TkError t.SrcPos e.Message) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos e.Message
    >;

  t.ErrorList (TkUnexpected t.SrcPos e.Unexpected) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos
      'Unknown characters "' e.Unexpected '"'
    >;

  t.ErrorList (s.Unexpected t.SrcPos e.Info) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos
      'Unexpected ' <StrFromToken s.Unexpected e.Info>
      ', expected ' e.Expected
    >;
}

$ENTRY EL-Destroy {
  (ErrorList e.Errors) = e.Errors;
}
