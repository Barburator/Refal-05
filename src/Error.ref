*$FROM Library
$EXTERN StrFromInt, Prout;

*$FROM Lexer
$EXTERN TkError, TkUnexpected, StrFromToken;

*$FROM LibraryEx
$EXTERN Map;


$ENUM ErrorList;

$ENTRY EL-Create {
  e.FileName = (ErrorList (e.FileName));
}

$ENTRY EL-AddError {
  (ErrorList (e.FileName) e.Errors) e.Message =
    (ErrorList (e.FileName) e.Errors ('  ERROR: ' e.Message));
}

$ENTRY EL-AddErrorAt {
  (ErrorList (e.FileName) e.Errors) t.SrcPos e.Message =
    (ErrorList
      (e.FileName)
      e.Errors
      (e.FileName ':' <StrFromSrcPos t.SrcPos> ':ERROR: ' e.Message)
    );
}

StrFromSrcPos {
  (s.Line s.Col) = <StrFromInt s.Line> ':' <StrFromInt s.Col>;
}

$ENTRY EL-AddUnexpected {
  t.ErrorList (TkError t.SrcPos e.Message) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos e.Message
    >;

  t.ErrorList (TkUnexpected t.SrcPos e.Unexpected) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos
      'Unknown characters "' e.Unexpected '"'
    >;

  t.ErrorList (s.Unexpected t.SrcPos e.Info) e.Expected =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos
      'Unexpected ' <StrFromToken s.Unexpected e.Info>
      ', expected ' e.Expected
    >;
}

$EENUM EL-NoErrors, EL-HasErrors;

$ENTRY EL-Destroy {
  (ErrorList (e.FileName)) = EL-NoErrors;

  (ErrorList (e.FileName) e.Errors) =
    <Map WriteBracketedLine e.Errors>
    EL-HasErrors;
}

WriteBracketedLine {
  (e.Line) = <Prout e.Line>;
}
