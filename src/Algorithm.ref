*$FROM LibraryEx
$EXTERN Fetch, Inc, Map, Seq, Dec;

*$FROM Lexer
$EXTERN TkVariable, TkCloseBracket, TkCloseCall, Add;

*$FROM Library
$EXTERN StrFromInt;

*$FROM Escape
$EXTERN EscapeChar;


$ENTRY MakeAlgorithm {
  (e.Pattern) (e.Result) =
    <GeneralizeResult
      <GenPattern e.Pattern>
      <GenResult e.Result>
    >;
}

/*
  e.Vars ::= (s.Count s.Mode e.Index)*
*/

*===============================================================================
* Генерация образца
*===============================================================================

* Направление распознавания
$EENUM AlgLeft, AlgRight;

$EENUM Brackets, CallBrackets, Symbol, Char, Number, Name;

/*
Команды распознавания
  Литералы, формат (Cmd*** s.Direction s.BracketNum e.Literal)
  Скобки, формат (CmdBrackets s.Direction s.BracketNum s.InnerNum)
  Пустые скобки, формат (CmdEmpty AlgLeft s.BracketNum),
    направление добавлено для единнобразия, генератором не используется
  Переменные:
    повторные, формат
      (CmdRepeated s.Direction s.BracketNum s.Usings s.Mode e.Index)
    новые s и t, формат (CmdVar s.Direction s.BracketNum s.Mode e.Index)
    открытые e:
      первоначальный формат (CmdOpenedE AlgLeft s.BracketNum 'e' e.Index)
      после обработки:
        (CmdOpenedE-Start AlgLeft s.BracketNum 'e' e.Index)
        (CmdOpenedE-End AlgLeft s.BracketNum 'e' e.Index)
    закрытые e, формат
      (CmdClosedE AlgLeft s.BracketNum 'e' e.Index (e.BracketsForSave))
  Команды сохранения скобок
    (CmdSave s.OldNumber e.NewNumber)
  Комментарий, вносимый в исходный код (CmdComment e.Text)
*/
$EENUM
  CmdSymbol, CmdBrackets, CmdEmpty,
  CmdRepeated, CmdVar, CmdClosedE,
  CmdOpenedE-Start, CmdOpenedE-End,
  CmdComment, CmdSave;

$ENUM CmdOpenedE;

GenPattern {
  e.Pattern =
    <DoGenPattern
      1 (0 e.Pattern) (/* vars */)
      (
        (CmdComment <TextFromPattern e.Pattern>)
        /* commands */
      )
    >;
}

DoGenPattern {
  /* Распознавание литералов (символьных, целочисленных, имён) */
  s.NumRanges e.Ranges-B (s.Num (Symbol s.Type e.Value) e.Range) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars) (e.Commands (CmdSymbol AlgLeft s.Num s.Type e.Value))
    >;

  s.NumRanges e.Ranges-B (s.Num e.Range (Symbol s.Type e.Value)) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars) (e.Commands (CmdSymbol AlgRight s.Num s.Type e.Value))
    >;

  /* Распознавание скобок */
  s.NumRanges e.Ranges-B
  (s.Num (Brackets e.SubRange) e.Range)
  e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      <Inc s.NumRanges>
      e.Ranges-B (s.NumRanges e.SubRange) (s.Num e.Range) e.Ranges-E
      (e.Vars) (e.Commands (CmdBrackets AlgLeft s.Num s.NumRanges))
    >;

  s.NumRanges e.Ranges-B
  (s.Num e.Range (Brackets e.SubRange))
  e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      <Inc s.NumRanges>
      e.Ranges-B (s.Num e.Range) (s.NumRanges e.SubRange) e.Ranges-E
      (e.Vars) (e.Commands (CmdBrackets AlgRight s.Num s.NumRanges))
    >;

  /* Пустой диапазон */
  s.NumRanges e.Ranges-B (s.Num /* пусто */) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B e.Ranges-E
      (e.Vars) (e.Commands (CmdEmpty AlgLeft s.Num))
    >;

  /* Распознавание переменных */
  s.NumRanges e.Ranges-B (s.Num (TkVariable s.Mode e.Index) e.Range) e.Ranges-E
  (e.Vars-B (s.Count s.Mode e.Index) e.Vars-E) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars-B (<Inc s.Count> s.Mode e.Index) e.Vars-E)
      (e.Commands (CmdRepeated AlgLeft s.Num <Inc s.Count> s.Mode e.Index))
    >;

  s.NumRanges e.Ranges-B (s.Num e.Range (TkVariable s.Mode e.Index)) e.Ranges-E
  (e.Vars-B (s.Count s.Mode e.Index) e.Vars-E) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars-B (<Inc s.Count> s.Mode e.Index) e.Vars-E)
      (e.Commands (CmdRepeated AlgRight s.Num <Inc s.Count> s.Mode e.Index))
    >;

  /* Диапазон с закрытой переменной */
  s.NumRanges e.Ranges-B (s.Num (TkVariable 'e' e.Index)) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B e.Ranges-E
      (e.Vars (1 'e' e.Index))
      (e.Commands (CmdClosedE AlgLeft s.Num 'e' e.Index))
    >;

  s.NumRanges e.Ranges-B (s.Num (TkVariable 's' e.Index) e.Range) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars (1 's' e.Index)) (e.Commands (CmdVar AlgLeft s.Num 's' e.Index))
    >;

  s.NumRanges e.Ranges-B (s.Num (TkVariable 't' e.Index) e.Range) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars (1 't' e.Index)) (e.Commands (CmdVar AlgLeft s.Num 't' e.Index))
    >;

  s.NumRanges e.Ranges-B (s.Num e.Range (TkVariable 's' e.Index)) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars (1 's' e.Index)) (e.Commands (CmdVar AlgRight s.Num 's' e.Index))
    >;

  s.NumRanges e.Ranges-B (s.Num e.Range (TkVariable 't' e.Index)) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      s.NumRanges e.Ranges-B (s.Num e.Range) e.Ranges-E
      (e.Vars (1 't' e.Index)) (e.Commands (CmdVar AlgRight s.Num 't' e.Index))
    >;

  s.NumRanges e.Ranges-B (s.Num (TkVariable 'e' e.Index) e.Range) e.Ranges-E
  (e.Vars) (e.Commands) =
    <DoGenPattern
      <SaveRanges
        s.NumRanges (/* scanned */) e.Ranges-B (s.Num e.Range) e.Ranges-E
        (e.Vars (1 'e' e.Index))
        (e.Commands (CmdOpenedE AlgLeft s.Num 'e' e.Index))
      >
    >;

  /* Завершение разбора. */
  s.NumRanges /* пусто */ (e.Vars) (e.Commands) =
    s.NumRanges (e.Vars) (e.Commands);
}

SaveRanges {
  s.NumRanges (e.ScannedRanges) (s.Num e.Range) e.Ranges
  (e.Vars) (e.Commands) =
    <SaveRanges
      <Inc s.NumRanges> (e.ScannedRanges (s.NumRanges e.Range)) e.Ranges
      (e.Vars) (e.Commands (CmdSave s.Num s.NumRanges))
    >;

  s.NumRanges (e.ScannedRanges) /* пусто */ (e.Vars) (e.Commands) =
    s.NumRanges e.ScannedRanges (e.Vars) (e.Commands);
}

TextFromPattern {
  (Brackets e.Inner) e.Tail =
    ' (' <TextFromPattern e.Inner> ' )' <TextFromPattern e.Tail>;

  (Symbol Char s.Char) e.Tail =
    ' \'' <TextFromPattern-Char (Symbol Char s.Char) e.Tail>;

  (Symbol Number s.Number) e.Tail =
    ' ' <StrFromInt s.Number> <TextFromPattern e.Tail>;

  (Symbol Name e.Name) e.Tail =
    ' & ' e.Name <TextFromPattern e.Tail>;

  (TkVariable s.Mode e.Index) e.Tail =
    ' ' s.Mode '.' e.Index <TextFromPattern e.Tail>;

  = ;
}

TextFromPattern-Char {
  (Symbol Char s.Char) e.Tail =
    <EscapeChar s.Char> <TextFromPattern-Char e.Tail>;

  = '\'';

  e.Other = <TextFromPattern e.Other>;
}

*===============================================================================
* Генерация результата
*===============================================================================

GenResult {
  e.Result =
    <DoGenResult
      (/* vars */)
      (/* alloc commands */) (/* other commands */)
      0 /* счётчик новых элементов */
      e.Result
    >;
}

* Основные команды работы с образцом
*  (CmdAllocateElem s.Number s.ElType e.Info)
*  (CmdLinkBrackets s.Left s.Right)
*  (CmdPushStack s.Number)
*  (CmdInsertElem s.Number)
*  (CmdInsertVar s.Usings s.Mode e.Index)
$EENUM
  CmdAllocateElem,
  CmdLinkBrackets,
  CmdPushStack,
  CmdInsertElem,
  CmdInsertVar,
  CmdInsertString;

* Спецификаторы команд (элементы)
$EENUM
  ElSymbol, ElString,
  ElOpenBracket, ElCloseBracket,
  ElOpenCall, ElCloseCall;

/*
  Порядок выполнения команд.
  1. Все выделения памяти (в порядке перечисления)
  2. Связывание крулых и угловых скобок и
  построение результата (в обратном порядке)
*/

$ENUM TkString;

DoGenResult {
  /* Оптимизация для распределения последовательности char’ов */
  (e.Vars) (e.AllocCommands) (e.Commands) s.Counter
  (Symbol Char s.Char1) (Symbol Char s.Char2) e.Result =
    <DoGenResult
      (e.Vars) (e.AllocCommands) (e.Commands) s.Counter
      (TkString 2 s.Char1 s.Char2) e.Result
    >;

  (e.Vars) (e.AllocCommands) (e.Commands)
  s.Counter (Symbol Char s.Value) e.Result =
    <DoGenResult
      (e.Vars)
      (e.AllocCommands (CmdAllocateElem s.Counter ElSymbol Char s.Value))
      ((CmdInsertElem s.Counter) e.Commands)
      <Inc s.Counter> e.Result
    >;

  (e.Vars) (e.AllocCommands) (e.Commands) s.Counter
  (TkString s.Len e.String) (Symbol Char s.Char) e.Result =
    <DoGenResult
      (e.Vars) (e.AllocCommands) (e.Commands) s.Counter
      (TkString <Inc s.Len> e.String s.Char) e.Result
    >;

  (e.Vars) (e.AllocCommands) (e.Commands) s.Counter
  (TkString s.Len e.String) e.Result =
    <DoGenResult
      (e.Vars)
      (e.AllocCommands (CmdAllocateElem s.Counter ElString s.Len e.String))
      ((CmdInsertString s.Counter) e.Commands)
      <Add 2 s.Counter> e.Result
    >;

  /* Создаём литералы */
  (e.Vars) (e.AllocCommands) (e.Commands)
  s.Counter (Symbol s.Type e.Value) e.Result =
    <DoGenResult
      (e.Vars)
      (e.AllocCommands (CmdAllocateElem s.Counter ElSymbol s.Type e.Value))
      ((CmdInsertElem s.Counter) e.Commands)
      <Inc s.Counter> e.Result
    >;

  /*
    Обработка открывающих скобок всех трёх типов осуществляется похоже.
  */
  (e.Vars) (e.AllocCommands) (e.Commands)
  s.Counter (Brackets e.InBrackets) e.Result =
    <DoGenResult
      (e.Vars)
      (e.AllocCommands (CmdAllocateElem s.Counter ElOpenBracket))
      ((CmdInsertElem s.Counter) e.Commands)
      <Inc s.Counter> e.InBrackets (TkCloseBracket s.Counter) e.Result
    >;

  (e.Vars) (e.AllocCommands) (e.Commands)
  s.Counter (CallBrackets e.InBrackets) e.Result =
    <DoGenResult
      (e.Vars)
      (e.AllocCommands (CmdAllocateElem s.Counter ElOpenCall))
      ((CmdInsertElem s.Counter) e.Commands)
      <Inc s.Counter> e.InBrackets (TkCloseCall s.Counter) e.Result
    >;

  /*
    Закрывающие скобки надо обрабатывать особым образом.
    Круглые скобки и АТД -- слинковать, угловые -- добавить в стек.
  */
  (e.Vars) (e.AllocCommands) (e.Commands)
  s.Counter (TkCloseBracket s.OpenCounter) e.Result =
    <DoGenResult
      (e.Vars)
      (e.AllocCommands (CmdAllocateElem s.Counter ElCloseBracket))
      (
        (CmdLinkBrackets s.OpenCounter s.Counter)
        (CmdInsertElem s.Counter)
        e.Commands
      )
      <Inc s.Counter> e.Result
    >;

  (e.Vars) (e.AllocCommands) (e.Commands)
  s.Counter (TkCloseCall s.OpenCounter) e.Result =
    <DoGenResult
      (e.Vars)
      (e.AllocCommands (CmdAllocateElem s.Counter ElCloseCall))
      (
        (CmdPushStack s.Counter)
        (CmdPushStack s.OpenCounter)
        (CmdInsertElem s.Counter)
        e.Commands
      )
      <Inc s.Counter> e.Result
    >;

  /* Обработка переменных */
  (e.Vars-B (s.Usings s.Mode e.Index) e.Vars-E) (e.AllocCommands) (e.Commands)
  s.Counter (TkVariable s.Mode e.Index) e.Result =
    <DoGenResult
      (e.Vars-B (<Inc s.Usings> s.Mode e.Index) e.Vars-E)
      (e.AllocCommands)
      ((CmdInsertVar <Inc s.Usings> s.Mode e.Index) e.Commands)
      s.Counter e.Result
    >;

  (e.Vars) (e.AllocCommands) (e.Commands)
  s.Counter (TkVariable s.Mode e.Index) e.Result =
    <DoGenResult
      (e.Vars (1 s.Mode e.Index))
      (e.AllocCommands)
      ((CmdInsertVar 1 s.Mode e.Index) e.Commands)
      s.Counter e.Result
    >;

  /* Завершение просмотра */
  (e.Vars) (e.AllocCommands) (e.Commands) s.Counter =
    s.Counter (e.Vars) (e.AllocCommands) (e.Commands);
}

*===============================================================================
* Обобщение информации -- уничтожение ненужных переменных,
* копирование повторных.
*===============================================================================

$EENUM CmdEmptyResult, CmdReturnResult;

GeneralizeResult {
  s.NumRanges (e.PatternVars) (e.PatternCommands)
  s.ResultCounter (e.ResultVars) (e.ResultAllocCommands) (e.ResultCommands) =
    <Fetch
      (<ReplicateVars e.PatternVars>)
      (<ReplicateVars e.ResultVars>)
      (<Map RepeatedEVariables e.PatternCommands>)
      <Map ClosedEVariables e.PatternCommands>
      <Seq
        MakeVarsSets
        (FilterCommonVarsAndPatternCommands (e.PatternCommands))
        (ComposeSentenceCommands
          s.NumRanges s.ResultCounter
          (e.ResultAllocCommands e.ResultCommands)
        )
      >
    >;
}

ReplicateVar {
  (1 s.Mode e.Index) = (1 s.Mode e.Index);

  (s.Using s.Mode e.Index) =
    <ReplicateVar (<Dec s.Using> s.Mode e.Index)>
    (s.Using s.Mode e.Index);
}

ReplicateVars {
  e.Vars = <Map ReplicateVar e.Vars>;
}

RepeatedEVariables {
  (CmdRepeated s.Direction s.Num s.Count 'e' e.Index) = (1 'e' e.Index);

  t.Other = /* игнорируем */;
}

ClosedEVariables {
  (CmdClosedE AlgLeft s.Num 'e' e.Index) = (1 'e' e.Index);

  t.Other = /* игнорируем */;
}

MakeVarsSets {
  (e.PatternVars) (e.ResultVars) (e.RepeatedEs) e.ClosedEs =
    (<VarSetUnion (e.PatternVars) (e.ResultVars)>)
    (<VarSetDifference (e.ResultVars) (e.PatternVars)>)
    <VarSetDifference (e.ClosedEs) (e.RepeatedEs e.ResultVars)>;
}

VarSetUnion {
  (e.Set1-B t.Common e.Set1-E) (e.Set2-B t.Common e.Set2-E) =
    t.Common <VarSetUnion (e.Set1-B e.Set1-E) (e.Set2-B e.Set2-E)>;

  (e.Set1) (e.Set2) = e.Set1 e.Set2;
}

VarSetDifference {
  (e.Set1-B t.Common e.Set1-E) (e.Set2-B t.Common e.Set2-E) =
    e.Set1-B <VarSetDifference (e.Set1-E) (e.Set2-B e.Set2-E)>;

  (e.Set1) (e.Set2) = e.Set1;
}

FilterCommonVarsAndPatternCommands {
  (e.PatternCommands)
  (e.CommonVars) (e.CopiedVars) e.UnusedClosedEs =
    (<VarSetDifference (e.CommonVars) (e.UnusedClosedEs)>)
    (e.CopiedVars)
    <Map (FilterUnusedCmdClosedE e.UnusedClosedEs) e.PatternCommands>;
}

FilterUnusedCmdClosedE {
  e.Unuseds-B (1 'e' e.Index) e.Unuseds-E
  (CmdClosedE AlgLeft s.Num 'e' e.Index) =
    (CmdComment ' Unused closed variable e.' e.Index);

  e.Unuseds t.OtherCommand = t.OtherCommand;
}

$EENUM CmdRangeArray, CmdInitB0, CmdResultArray;

ComposeSentenceCommands {
  s.NumRanges s.ResultCounter
  (e.ResultCommands) (e.CommonVars) (e.CopiedVars) e.PatternCommands =
    <GenerateResult-OpenELoops
      <Map MakeDeclaration e.CommonVars>
      (CmdRangeArray s.NumRanges)
      <MakeCmdResultCommand s.ResultCounter>
      (CmdInitB0)
      e.PatternCommands
      (CmdEmptyResult)
      <Map MakeCopyVar e.CopiedVars>
      e.ResultCommands
      (CmdReturnResult)
    >;
}

$EENUM CmdDeclareEVar, CmdDeclareVar;

MakeDeclaration {
  (s.Usings 'e' e.Index) = (CmdDeclareEVar s.Usings 'e' e.Index);
  (s.Usings s.Mode e.Index) = (CmdDeclareVar s.Usings s.Mode e.Index);
}

$EENUM CmdCopyEVar, CmdCopyVar;

MakeCopyVar {
  (s.Usings 'e' e.Index) = (CmdCopyEVar s.Usings 'e' e.Index);
  (s.Usings s.Mode e.Index) = (CmdCopyVar s.Usings s.Mode e.Index);
}

MakeCmdResultCommand {
  0 = /* пусто */;
  s.Counter = (CmdResultArray s.Counter);
}

GenerateResult-OpenELoops {
  e.Commands-B (CmdOpenedE AlgLeft s.BracketNum 'e' e.Index) e.Commands-E =
    e.Commands-B
    (CmdOpenedE-Start AlgLeft s.BracketNum 'e' e.Index)
    <GenerateResult-OpenELoops e.Commands-E>
    (CmdOpenedE-End AlgLeft s.BracketNum 'e' e.Index);

  e.Commands = e.Commands;
}
