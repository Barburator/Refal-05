/**
  <ParseCommandLine e.Arguments>
    == t.Compiler (e.Folders) t.FoundedFile*
    == (CmdLineError e.Message)

  t.FoundedFile -- см. в FindFile
  t.Compiler ::= (NoCompile) | (CompileCommand e.Command)
*/
$ENTRY ParseCommandLine {
  e.Arguments =
    <SwCompileCommand <GetEnv 'R05CCOMP'>>
    (<ParsePath <GetEnv 'R05PATH'>>)
    <DoParseCommandLine (/* files */) e.Arguments>;
}

SwCompileCommand {
  /* пусто */ = (NoCompile);
  e.Command = (CompileCommand e.Command);
}

ParsePath {
  e.Folder ';' e.Path = <ParseFolder e.Folder> <ParsePath e.Path>;
  e.Folder = <ParseFolder e.Folder>;
}

ParseFolder {
  /* пусто */ = /* пусто */;
  e.Folder = (e.Folder);
}

*$EENUM NoCompile, CompileCommand, CmdLineError

DoParseCommandLine {
  (e.ScannedFiles) ('++') e.Files =
    <DoParseFileNamesOnly
      (e.ScannedFiles) e.Files
    >;

  (e.ScannedFiles) (e.NextFileName) e.Options =
    <DoParseCommandLine
      (e.ScannedFiles (e.NextFileName)) e.Options
    >;

  (e.ScannedFiles) =
    e.ScannedFiles;
}

DoParseFileNamesOnly {
  (e.ScannedFiles) (e.NextFileName) e.Files =
    <DoParseFileNamesOnly
      (e.ScannedFiles (e.NextFileName)) e.Files
    >;

  (e.ScannedFiles) =
    e.ScannedFiles;
}
