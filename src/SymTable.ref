*$FROM LibraryEx
$EXTERN Map, Reduce;

*$FROM Error
$EXTERN EL-AddErrorAt;

*$FROM Generator
$EXTERN GN-Entry;


*$ENUM SymTable

$ENTRY ST-Create {
  = (SymTable <LoadBuiltins> ());
}

LoadBuiltins {
  = <Map symtable_BuiltinDeclaration <ListOfBuiltin>>;
}

$ENTRY symtable_BuiltinDeclaration {
  (s.Number s.Name s.Type) = <Normalize <Explode s.Name>>;
}

Normalize {
  /* пусто */ = /* пропускаем */;
  e.Name '$' = /* пропускаем */;

  e.Name-B '-' e.Name-E = <Normalize e.Name-B '_' e.Name-E>;

  e.Name = (Declared e.Name);
}

$ENTRY ST-Destroy {
  (SymTable e.Names (e.References)) = ;
}

/*
  Внутренний формат
  t.SymTable ::= (SymTable t.Name* (t.Reference*))
  t.Name ::=
      (Declared e.Name)
    | (Defined s.ScopeClass e.Name)
  t.Reference ::= (t.SrcPos e.Name)
*/

*$ENUM Declared, Defined

PatchReferences {
  (e.References) e.Name =
    (<Map (symtable_PatchReferences-FilterThis e.Name) e.References>);
}

$ENTRY symtable_PatchReferences-FilterThis {
  e.Name (t.SrcPos e.Name) = /* пусто */;

  e.Name (t.SrcPos e.OtherName) = (t.SrcPos e.OtherName);
}

/**
  <ST-AddDefined t.ErrorList t.SymTable s.ScopeClass t.SrcPos e.Name>
    == t.ErrorList t.SymTable
*/
$ENTRY ST-AddDefined {
  t.ErrorList
  (SymTable
    e.Names-B (Defined s.AnyScopeClass e.Name) e.Names-E (e.References)
  )
  s.ScopeClass t.SrcPos e.Name =
    <EL-AddErrorAt t.ErrorList t.SrcPos 'Function ' e.Name ' already defined'>
    (SymTable
      e.Names-B (Defined s.AnyScopeClass e.Name) e.Names-E (e.References)
    );

  t.ErrorList
  (SymTable e.Names-B (Declared e.Name) e.Names-E (e.References))
  s.ScopeClass t.SrcPos e.Name =
    t.ErrorList
    (SymTable
      e.Names-B (Defined s.ScopeClass e.Name) e.Names-E
      <PatchReferences (e.References) e.Name>
    );

  t.ErrorList
  (SymTable e.Names (e.References)) s.ScopeClass t.SrcPos e.Name =
    t.ErrorList
    (SymTable
      e.Names (Defined s.ScopeClass e.Name)
      <PatchReferences (e.References) e.Name>
    );
}

/**
  <ST-AddDeclared t.SymTable e.Name> == t.SymTable
*/
$ENTRY ST-AddDeclared {
  /*
    Можно повторно объявить имя не зависимо от того, было ли оно до этого
    объявлено или определено.
  */
  (SymTable e.Names-B (Declared e.Name) e.Names-E (e.References)) e.Name =
    (SymTable e.Names-B (Declared e.Name) e.Names-E (e.References));

  (SymTable e.Names-B (Defined s.ScopeClass e.Name) e.Names-E (e.References))
  e.Name =
    (SymTable e.Names-B (Defined s.ScopeClass e.Name) e.Names-E (e.References));

  (SymTable e.Names (e.References)) e.Name =
    (SymTable e.Names (Declared e.Name) <PatchReferences (e.References) e.Name>);
}

/**
  <ST-AddFunctionCall t.SymTable t.SrcPos e.Name> == t.SymTable
*/
$ENTRY ST-AddFunctionCall {
  (SymTable e.Names-B (Declared e.Name) e.Names-E (e.References))
  t.SrcPos e.Name =
    (SymTable e.Names-B (Declared e.Name) e.Names-E (e.References));

  (SymTable e.Names-B (Defined s.ScopeClass e.Name) e.Names-E (e.References))
  t.SrcPos e.Name =
    (SymTable e.Names-B (Defined s.ScopeClass e.Name) e.Names-E (e.References));

  (SymTable e.Names (e.References))
  t.SrcPos e.Name =
    (SymTable e.Names (e.References (t.SrcPos e.Name)));
}

/**
  <ST-CheckUnresolved t.ErrorList t.SymTable>
    == t.ErrorList t.SymTable
*/
$ENTRY ST-CheckUnresolved {
  t.ErrorList (SymTable e.Names (e.References)) =
    <Reduce symtable_AddUnresolved t.ErrorList e.References>
    (SymTable e.Names (e.References));
}

$ENTRY symtable_AddUnresolved {
  t.ErrorList (t.SrcPos e.Name) =
    <EL-AddErrorAt
      t.ErrorList t.SrcPos 'Function ' e.Name ' is not defined'
    >;
}

/**
  <ST-AllFunctions t.SymTable>
    == (s.ScopeClass e.Name)*
*/
$ENTRY ST-AllFunctions {
  (SymTable e.Names (e.References)) = <Map symtable_FunctionFromKnown e.Names>;
}

$ENTRY symtable_FunctionFromKnown {
  (Declared e.Name) = (GN-Entry e.Name);
  (Defined s.ScopeClass e.Name) = (s.ScopeClass e.Name);
}
