*$FROM LibraryEx
$EXTERN ArgList, LoadFile, Map, Success, Fails;

*$FROM R05-FindFile
$EXTERN FindFiles, NotFound, Output, Source;

*$FROM R05-Parser
$EXTERN R05-Parse;

*$FROM R05-Generator
$EXTERN R05-Generate;


$ENTRY Go {
  = <Main <ArgList>>;
}

Main {
  (e.ProgName) ('@' e.Config) =
    <Main (e.ProgName) <LoadFile e.Config>>;

  (e.ProgName) e.Files =
    <FindFiles-SwFound
      <FindFiles (<LoadPath>) e.Files>
    >;
}

LoadPath {
  = <ParsePath <GetEnv 'R05PATH'>>
}

ParsePath {
  e.Folder ';' e.Path = <ParseFolder e.Folder> <ParsePath e.Path>;
  e.Folder = <ParseFolder e.Folder>;
}

ParseFolder {
  /* пусто */ = /* пусто */;
  e.Folder = (e.Folder);
}

FindFiles-SwFound {
  e.Files-B (NotFound e.FileName) e.Files-E =
    <Map refal05c_PrintNotFound (NotFound e.FileName) e.Files-E>;

  e.FoundFiles =
    <CheckCompilationResult
      <Map refal05c_ProcessEachSource e.FoundFiles>
    >;
}

$ENTRY refal05c_PrintNotFound {
  (NotFound e.FileName) =
    <Prout 'COMMAND LINE ERROR: file ' e.FileName ' not found'>;

  (Output e.FileName) = ;

  (Source (e.Source) e.Output) = ;
}

$ENTRY refal05c_ProcessEachSource {
  (Output e.OutputName) =
    <Prout '+Linking ' e.OutputName> (e.OutputName);

  (Source (e.Source) e.OutputName) =
    <Prout '*Compiling ' e.Source ':'>
    <CompileSource-SwSuccessedParse
      (e.Source) (e.OutputName) <R05-Parse e.Source>
    >
}

CompileSource-SwSuccessedParse {
  (e.SrcName) (e.OutputName) Success e.Tree =
    <R05-Generate (e.OutputName) e.Tree>
    (e.OutputName);

  (e.SrcName) (e.OutputName) Fails e.Errors =
    <Map (refal05c_WriteError e.SrcName) e.Errors>
    Fails;
}

$ENTRY refal05c_WriteError {
  e.FileName (t.SrcPos e.Message) =
    <Prout e.FileName ':' <StrFromSrcPos t.SrcPos> ':ERROR: ' e.Message>;
}

StrFromSrcPos {
  (s.Line s.Col) = <Symb s.Line> ':' <Symb s.Col>;
}


CheckCompilationResult {
  e.Outputs-B Fails e.Outputs-E =
    <Prout '*** COMPILATION FAILED ***'>
    <Exit 1>;

  e.Outputs =
    <Link (<GetEnv 'R05CCOMP'>) e.Outputs>;
}

Link {
  (/* не установлена R05CCOMP */) e.Files =
    <Prout '*** Compilation successed ***'>;

  (e.CommandLine) e.Files =
    <CheckRetcode
      <System
        e.CommandLine ' ' <GetEnv 'R05CFLAGS'>
        <Map refal05c_IncludeFlag <LoadPath>>
        <Map refal05c_QuoteFile e.Files>>
    >;
}

CheckRetcode {
  0 = <Prout '*** Compilation successed ***'>;

  e.RetCode =
    <Prout
      '*** COMPILATION FAILED '
      '(C COMPILER FAILED, RETCODE: ' <Symb e.RetCode>')***'
    >
    <Exit e.RetCode>;
}

$ENTRY refal05c_IncludeFlag {
  (e.PathEntry) = ' -I"' e.PathEntry '"';
}

$ENTRY refal05c_QuoteFile {
  (e.FileName) = ' "' e.FileName '"';
}
