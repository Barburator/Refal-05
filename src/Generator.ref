*$FROM LibraryEx
$EXTERN MapAccum, Map, Dec, DelAccumulator, Inc;

*$FROM Sentence
$EXTERN
  CmdComment, CmdDeclareVar, CmdRangeArray, CmdInitB0, CmdMatch, CmdEmpty,
  CmdClosedE, CmdOpenedE-Start, CmdOpenedE-End, CmdSave, CmdEmptyResult,
  CmdResultArray, CmdAllocateElem, CmdLinkBrackets, CmdPushStack, CmdInsertVar,
  CmdReturnResult, Symbol, Brackets, Repeated, Variable, Char, Number, Name,
  AlgLeft, AlgRight, ElSymbol, ElString, ElOpenBracket, ElCloseBracket,
  ElOpenCall, ElCloseCall, ElSavePos, ElVariable;

*$FROM Escape
$EXTERN EscapeChar;


*$EENUM GN-Local, GN-Entry

TextFromMemoryClass {
  GN-Local = 'static '; GN-Entry = ;
}

$ENTRY GenFnStart {
  s.MemoryClass e.Name =
    (
      'static void r05c_' e.Name
      '(struct r05_node *arg_begin, struct r05_node *arg_end) {'
    );
}

$ENTRY GenThisIsGeneratedFunction {
  = ('  r05_this_is_generated_function();');
}

$ENTRY GenFail {
  = () ('  r05_recognition_impossible();')
}

$ENTRY GenFnEnd {
  s.MemoryClass e.Name =
    ('}')
    (
      <TextFromMemoryClass s.MemoryClass>
      'struct r05_function r05f_' e.Name ' = { r05c_' e.Name ', "' e.Name '" };'
    )
    ();
}

$ENTRY GenEnum {
  s.ScopeClass e.Name =
    (
      <TextFromMemoryClass s.ScopeClass>
      'struct r05_function r05f_' e.Name ' = '
      '{ r05_enum_function_code, "' e.Name '" };'
    )
    ();
}

$ENTRY GenDeclaration {
  (GN-Entry e.Name) = ('extern struct r05_function r05f_' e.Name ';');
  (GN-Local e.Name) = ('static struct r05_function r05f_' e.Name ';');
}

GenCommonHeaders {
  = ('/* Automatically generated file. Don\'t edit! */')
  ('#include "refalrts.h"')
  () ();
}

GenCommonTailer {
  = () ('/* End of file */');
}

$ENTRY GenSentence {
  e.SentenceCommands =
    ()
    ('  do {')
    <SkipIndentAccum
      <MapAccum generator_GenCommand ('    ' /* отступ */) e.SentenceCommands>
    >
    ('  } while (0);');
}

SkipIndentAccum {
  /* отступ должен остаться тем же */
  ('    ') e.Generated = e.Generated;
}

BracketsVar-B {
  s.BracketNumber = 'bb[' <Symb s.BracketNumber> ']';
}

BracketsVar-E {
  s.BracketNumber = 'be[' <Symb s.BracketNumber> ']';
}

BracketsVars {
  s.BracketNumber =
    <BracketsVar-B s.BracketNumber> ', ' <BracketsVar-E s.BracketNumber>;
}

BracketsVarsPtr {
  s.BracketNumber = 'bb+' <Symb s.BracketNumber> ', be+' <Symb s.BracketNumber>;
}

SafeComment {
  e.Comment-B '/*' e.Comment-E =
    <SafeComment e.Comment-B '/\' \'*' e.Comment-E>;

  e.Comment-B '*/' e.Comment-E =
    <SafeComment e.Comment-B '*\' \'/' e.Comment-E>;

  e.Comment = e.Comment;
}

$ENTRY generator_GenCommand {
  (e.Indent) (CmdComment e.Text) =
    (e.Indent) (e.Indent '/*' <SafeComment e.Text> '*/');

  (e.Indent) (CmdDeclareVar s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent 'struct r05_node *' <PrintEVar-B s.Usings 'e' e.Index> ';')
    (e.Indent 'struct r05_node *' <PrintEVar-E s.Usings 'e' e.Index> ';');

  (e.Indent) (CmdDeclareVar s.Usings s.Mode e.Index) =
    (e.Indent)
    (e.Indent 'struct r05_node *' <PrintVar s.Usings s.Mode e.Index> ';');

  (e.Indent) (CmdRangeArray s.Size) =
    (e.Indent)
    (e.Indent 'struct r05_node *bb[' <Symb s.Size> '] = { 0 };')
    (e.Indent 'struct r05_node *be[' <Symb s.Size> '] = { 0 };');

  (e.Indent) (CmdInitB0) =
    (e.Indent)
    (e.Indent 'r05_start_sentence();')
    (e.Indent 'r05_prepare_argument(' <BracketsVarsPtr 0> ', arg_begin, arg_end);');

  (e.Indent) (CmdMatch s.Direction s.BracketNumber s.Type e.Value) =
    (e.Indent)
    (e.Indent
      'if (! r05_' <MatchFunc s.Type e.Value> '_' <StrFromDirection s.Direction>
      '(' <MatchRep s.Type e.Value> ', ' <BracketsVarsPtr s.BracketNumber> '))'
    )
    (e.Indent '  continue;');

  (e.Indent) (CmdEmpty s.BracketNumber) =
    (e.Indent)
    (e.Indent
      'if (! r05_empty_seq(' <BracketsVars s.BracketNumber> '))'
    )
    (e.Indent '  continue;');

  (e.Indent) (CmdClosedE s.BracketNumber 'e' e.Index) =
    (e.Indent)
    (e.Indent
      <PrintEVar-B 1 'e' e.Index> ' = ' <BracketsVar-B s.BracketNumber> ';'
    )
    (e.Indent
      <PrintEVar-E 1 'e' e.Index> ' = ' <BracketsVar-E s.BracketNumber> ';'
    );

  (e.Indent)
  (CmdOpenedE-Start s.BracketNumber 'e' e.Index) =
    (e.Indent '  ')
    (e.Indent <PrintEVar-B 1 'e' e.Index> ' = 0;')
    (e.Indent <PrintEVar-E 1 'e' e.Index> ' = 0;')
    (e.Indent 'r05_start_e_loop();')
    (e.Indent 'do {');

  (e.Indent '  ')
  (CmdOpenedE-End s.BracketNumber 'e' e.Index) =
    (e.Indent)
    (e.Indent
      '} while (r05_open_evar_advance('
      <PrintEVarsPtr 1 'e' e.Index> ', ' <BracketsVarsPtr s.BracketNumber>
      '));'
    );

  (e.Indent) (CmdSave s.Old s.New) =
    (e.Indent)
    (e.Indent <BracketsVar-B s.New> ' = ' <BracketsVar-B s.Old> ';')
    (e.Indent <BracketsVar-E s.New> ' = ' <BracketsVar-E s.Old> ';');

  (e.Indent) (CmdEmptyResult) =
    (e.Indent)
    ()
    (e.Indent 'r05_reset_allocator();');

  (e.Indent) (CmdResultArray s.Size) =
    (e.Indent)
    (e.Indent 'struct r05_node *n[' <Symb s.Size> '] = { 0 };');

  (e.Indent) (CmdAllocateElem e.ElemInfo) =
    (e.Indent)
    <PrintAllocateElem (e.Indent) e.ElemInfo>;

  (e.Indent) (CmdLinkBrackets s.Left s.Right) =
    (e.Indent)
    (e.Indent
      'r05_link_brackets'
      '(' <PrintElemVar s.Left> ', ' <PrintElemVar s.Right> ');'
    );

  (e.Indent) (CmdPushStack s.ElemNumber) =
    (e.Indent)
    (e.Indent 'r05_push_stack(' <PrintElemVar s.ElemNumber> ');');

  (e.Indent) (CmdInsertVar s.Pos s.Usings 'e' e.Index) =
    (e.Indent)
    (e.Indent
      'r05_splice_evar(' <PrintElemVar s.Pos> ', '
      <PrintEVars s.Usings 'e' e.Index> ');'
    );

  (e.Indent) (CmdInsertVar s.Pos s.Usings 't' e.Index) =
    (e.Indent)
    (e.Indent
      'r05_splice_tvar(' <PrintElemVar s.Pos> ', '
      <PrintVar s.Usings 't' e.Index> ');'
    );

  (e.Indent) (CmdReturnResult) =
    (e.Indent)
    (e.Indent 'r05_splice_from_freelist(arg_begin);')
    (e.Indent 'r05_splice_to_freelist(arg_begin, arg_end);')
    (e.Indent 'return;');
}

EscapeString {
  e.String = <Map EscapeChar e.String>;
}

PrintVar {
  s.Usings s.Mode e.Index = s.Mode e.Index '_' <Symb s.Usings>;
}

PrintVarPtr {
  s.Usings s.Mode e.Index = '&' s.Mode e.Index '_' <Symb s.Usings>;
}

PrintEVar-B {
  s.Usings s.Mode e.Index = s.Mode e.Index '_b_' <Symb s.Usings>;
}

PrintEVar-E {
  s.Usings s.Mode e.Index = s.Mode e.Index '_e_' <Symb s.Usings>;
}

PrintEVars {
  s.Usings s.Mode e.Index =
    <PrintEVar-B s.Usings s.Mode e.Index> ', '
    <PrintEVar-E s.Usings s.Mode e.Index>;
}

PrintEVarsPtr {
  s.Usings s.Mode e.Index =
    '&' <PrintEVar-B s.Usings s.Mode e.Index> ', '
    '&' <PrintEVar-E s.Usings s.Mode e.Index>;
}

MatchFunc {
  Symbol s.Type e.Value = <SymbolFunc s.Type>;
  Brackets s.InnerNum = 'brackets';
  Repeated s.Usings 'e' e.Index = 'repeated_evar';
  Repeated s.Usings s.Mode e.Index = 'repeated_stvar';
  Variable 's' e.Index = 'svar';
  Variable 't' e.Index = 'tvar';
}

MatchRep {
  Symbol s.Type e.Value = <SymbolTextRep s.Type e.Value>;

  Brackets s.InnerNum = <BracketsVarsPtr s.InnerNum>;

  Repeated s.Usings 'e' e.Index =
    <PrintEVarsPtr s.Usings 'e' e.Index> ', '
    <PrintEVars <Dec s.Usings> 'e' e.Index>;

  Repeated s.Usings s.Mode e.Index =
    <PrintVarPtr s.Usings s.Mode e.Index> ', '
    <PrintVar <Dec s.Usings> s.Mode e.Index>;

  Variable s.Mode e.Index = <PrintVarPtr 1 s.Mode e.Index>;
}

SymbolFunc {
  Char = 'char';
  Number = 'number';
  Name = 'function';
}

SymbolTextRep {
  Char s.Char = '\'' <EscapeChar s.Char> '\'';
  Number s.Number = <Symb s.Number> 'UL';
  Name e.Name = '&r05f_' e.Name;
}

StrFromDirection {
  AlgLeft = 'left'; AlgRight = 'right';
}

PrintElemVar {
  s.Number = 'n[' <Symb s.Number> ']';
}

PrintElemVarPtr {
  s.Number = 'n+' <Symb s.Number>;
}

PrintAllocateElem {
  (e.Indent) s.ElementType e.Info =
    (e.Indent
      'r05_alloc_' <SwAllocator s.ElementType e.Info>
      '(' <SwInfo s.ElementType e.Info> ');'
    );
}

SwAllocator {
  ElSymbol s.Type e.Value = <SymbolFunc s.Type>;
  ElString s.Len e.String = 'chars';

  ElOpenBracket s.Pos = 'open_bracket';
  ElCloseBracket s.Pos = 'close_bracket';

  ElOpenCall s.Pos = 'open_call';
  ElCloseCall s.Pos = 'close_call';

  ElSavePos s.Pos = 'insert_pos';

  ElVariable s.Mode e.Index = s.Mode 'var';
}

SwInfo {
  ElSymbol s.Type e.Value = <SymbolTextRep s.Type e.Value>;
  ElString s.Len e.String = '"' <EscapeString e.String> '", ' <Symb s.Len>;

  ElOpenBracket s.Pos = <PrintElemVarPtr s.Pos>;
  ElCloseBracket s.Pos = <PrintElemVarPtr s.Pos>;

  ElOpenCall s.Pos = <PrintElemVarPtr s.Pos>;
  ElCloseCall s.Pos = <PrintElemVarPtr s.Pos>;

  ElSavePos s.Pos = <PrintElemVarPtr s.Pos>;

  ElVariable 'e' e.Index = <PrintEVars 1 'e' e.Index>;
  ElVariable s.Mode e.Index = <PrintVar 1 s.Mode e.Index>;
}

*$ENUM BeginNative, EndNative

$ENTRY GenNative {
  s.NativeLineNo e.Code = (BeginNative s.NativeLineNo) e.Code (EndNative);
}

$ENTRY GenPostprocess {
  (e.SrcName) (e.OutputName) e.Lines-B (EndNative) e.Lines-E =
    <DelAccumulator
      <MapAccum
        (generator_EnumerateLines (e.SrcName) (e.OutputName))
        1
        <GenCommonHeaders>
        e.Lines-B (EndNative) e.Lines-E
        <GenCommonTailer>
      >
    >;

  (e.SrcName) (e.OutputName) e.Lines =
    <GenCommonHeaders>
    e.Lines
    <GenCommonTailer>;
}

$ENTRY generator_EnumerateLines {
  (e.SrcName) (e.OutputName) s.LineNo (BeginNative s.NativeLineNo) =
    <Inc s.LineNo>
    <LineDirective s.NativeLineNo e.SrcName>;

  (e.SrcName) (e.OutputName) s.LineNo (EndNative) =
    <Inc s.LineNo>
    <LineDirective s.LineNo e.OutputName>;

  (e.SrcName) (e.OutputName) s.LineNo (e.Line) = <Inc s.LineNo> (e.Line);
}

LineDirective {
  s.LineNo e.FileName =
    ('#line ' <Symb <Inc s.LineNo>> ' "' <EscapeString e.FileName> '"');
}
